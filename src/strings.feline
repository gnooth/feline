// Copyright (C) 2019 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feline ;
in: feline

private: string-trim-head-internal ( s ) // string -> string'
    s [ whitespace? not ] find // -> index/f element/f
    [ s string-tail ] [ drop "" ] if ;

: string-trim-head ( s ) // string -> string'
    s string-length zero? [ s ] return-if
    s string-first-char whitespace? not [ s ] return-if
    string-trim-head-internal ;

private: string-trim-tail-internal ( s ) // string -> string'
    s string-length 1- s [ whitespace? not ] find-last-from // -> index/f element/f
    [ 1+ s string-head ] [ drop "" ] if ;

: string-trim-tail ( s ) // string -> string'
    s string-length zero? [ s ] return-if
    s string-last-char whitespace? not [ s ] return-if
    string-trim-tail-internal ;

: string-trim // string -> string'
    !> s
    s string-length zero? [ s ] return-if
    s string-first-char whitespace? [ s string-trim-head-internal s! ] when
    s string-length zero? [ s ] return-if
    s string-last-char whitespace? [ s string-trim-tail-internal s! ] when
    s ;

