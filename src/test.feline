using: feline ;
in: user

"test" delete-vocab

in: test

-- this is a comment
( this is another comment )

{- yet another comment -}

interactive? get assert-false

1 2 + 3 eq? assert-true
1 2 + 4 eq? assert-false

"test" "test" eq? assert-false

V{ 1 2 3 } length 3 assert=
V{ "this is a test" } length 1 assert=

V{ 1 2 3 } [ dup + ] map V{ 2 4 6 } sequence= assert-true

"this is a multiline
string" length 26 assert=

clear

test: multiline-string-test-1 ( -- )
    "this is
a multiline string"
    dup string? assert-true
    length 26 assert=
;

multiline-string-test-1

test: string?-test-1 ( -- )
    t string? assert-false
    f string? assert-false
    42 string? assert-false
    2000000 string? assert-false
    "test" string? assert-true
    \ dup string? assert-false
    \ dup symbol-name string? assert-true
;

string?-test-1

-- Test for the Factor/Feline version of pick.
-- The Forth version of pick is different.
test: pick-test-1 ( -- )
    clear
    1 2 3 pick get-datastack { 1 2 3 1 } sequence= assert-true
    clear
;

pick-test-1

-- Test for the Factor/Feline version of 2over.
-- The Forth version of 2over is different.
test: 2over-test-1 ( -- )
    clear
    1 2 3 2over get-datastack { 1 2 3 1 2 } sequence= assert-true
    clear
;

2over-test-1

test: fixnum?-test-1 ( -- )
    most-negative-fixnum fixnum? assert-true
    -3 fixnum? assert-true
    -2 fixnum? assert-true
    -1 fixnum? assert-true
    0 fixnum? assert-true
    1 fixnum? assert-true
    2 fixnum? assert-true
    3 fixnum? assert-true
    most-positive-fixnum fixnum? assert-true
    "test" fixnum? assert-false
    \ dup fixnum? assert-false
    { 1 2 3 } fixnum? assert-false
;

fixnum?-test-1

test: fixnum-test-1 ( -- )
    1 2 + 3 assert=
    1 2 + fixnum? assert-true
;

fixnum-test-1

test: fixnum-test-2 ( -- )
    -1 -2 + -3 assert=
;

fixnum-test-2

test: fixnum-test-3 ( -- )
    0 fixnum? assert-true
    0 0 assert=
    1 0 = assert-false
    -1 0 = assert-false
    1 -1 + 0 assert=
    4 -4 + 0 assert=
;

fixnum-test-3

-- fixnum<
test: fixnum<-test-1 ( -- )
    0 0 fixnum< assert-false
    1 1 fixnum< assert-false
    1 2 fixnum< assert-true
    2 1 fixnum< assert-false
    -2 -1 fixnum< assert-true
    -1 -2 fixnum< assert-false
    -2 1 fixnum< assert-true
    1 -2 fixnum< assert-false
    most-negative-fixnum 0 fixnum< assert-true
    most-negative-fixnum most-positive-fixnum fixnum< assert-true
    0 most-positive-fixnum fixnum< assert-true
;

fixnum<-test-1

-- fixnum<=
test: fixnum<=-test-1 ( -- )
    0 0 fixnum<= assert-true
    1 1 fixnum<= assert-true
    1 2 fixnum<= assert-true
    2 1 fixnum<= assert-false
    -2 -1 fixnum<= assert-true
    -1 -2 fixnum<= assert-false
    -2 1 fixnum<= assert-true
    1 -2 fixnum<= assert-false
    most-negative-fixnum 0 fixnum<= assert-true
    most-negative-fixnum most-positive-fixnum fixnum<= assert-true
    most-negative-fixnum most-negative-fixnum fixnum<= assert-true
    most-positive-fixnum most-positive-fixnum fixnum<= assert-true
    0 most-positive-fixnum fixnum<= assert-true
;

fixnum<=-test-1

-- fixnum>
test: fixnum>-test-1 ( -- )
    0 0 fixnum> assert-false
    1 1 fixnum> assert-false
    1 2 fixnum> assert-false
    2 1 fixnum> assert-true
    -2 -1 fixnum> assert-false
    -1 -2 fixnum> assert-true
    -2 1 fixnum> assert-false
    1 -2 fixnum> assert-true
    most-negative-fixnum 0 fixnum> assert-false
    most-negative-fixnum most-positive-fixnum fixnum> assert-false
    most-positive-fixnum most-negative-fixnum fixnum> assert-true
    0 most-positive-fixnum fixnum> assert-false
;

fixnum>-test-1

-- fixnum>=
test: fixnum>=-test-1 ( -- )
    0 0 fixnum>= assert-true
    1 1 fixnum>= assert-true
    1 2 fixnum>= assert-false
    2 1 fixnum>= assert-true
    -2 -1 fixnum>= assert-false
    -1 -2 fixnum>= assert-true
    -2 1 fixnum>= assert-false
    1 -2 fixnum>= assert-true
    most-negative-fixnum 0 fixnum>= assert-false
    most-negative-fixnum most-positive-fixnum fixnum>= assert-false
    most-negative-fixnum most-negative-fixnum fixnum>= assert-true
    most-positive-fixnum most-positive-fixnum fixnum>= assert-true
    0 most-positive-fixnum fixnum>= assert-false
;

fixnum>=-test-1

-- >=
test: >=-test-1 ( -- )
    0 0 >= assert-true
    1 1 >= assert-true
    1 2 >= assert-false
    2 1 >= assert-true
    -2 -1 >= assert-false
    -1 -2 >= assert-true
    -2 1 >= assert-false
    1 -2 >= assert-true
    most-negative-fixnum 0 >= assert-false
    most-negative-fixnum most-positive-fixnum >= assert-false
    most-negative-fixnum most-negative-fixnum >= assert-true
    most-positive-fixnum most-positive-fixnum >= assert-true
    0 most-positive-fixnum >= assert-false
;

>=-test-1

test: fixnum-test-5 ( -- )
    1 2 fixnum- -1 assert=
    1 2 - -1 assert=
;

fixnum-test-5

test: fixnum-test-6 ( -- )
    2 3 fixnum* 6 assert=
    2 3 * 6 assert=
;

fixnum-test-6

test: fixnum-test-7 ( -- )
    8 3 fixnum/i 2 assert=
    8 3 / 2 assert=
    -8 3 fixnum/i -2 assert=
    -8 3 / -2 assert=
;

fixnum-test-7

test: fixnum-test-8 ( -- )
    7 3 fixnum-mod 1 assert=
    7 3 mod 1 assert=
    -7 3 fixnum-mod -1 assert=
    -7 3 mod -1 assert=
;

fixnum-test-8

-- fixnum-bitand, bitand
test: fixnum-test-9 ( -- )
    15 7 bitand 7 assert=
    15 $f0 bitand 0 assert=
    3 1 bitand 1 assert=
;

fixnum-test-9

-- fixnum+
test: fixnum-test-10 ( -- )
    42 87 fixnum+ 129 assert=
    -42 87 fixnum+ 45 assert=
    42 -87 fixnum+ -45 assert=
    -42 -87 fixnum+ -129 assert=
;

fixnum-test-10

test: fixnum>string-test-1 ( -- )
    -17 fixnum>string "-17" assert=
    0 fixnum>string "0" assert=
    42 fixnum>string "42" assert=
    tag-bits 3 assert=
    most-negative-fixnum fixnum>string "-1152921504606846976" assert=
    most-negative-fixnum 1 + fixnum>string "-1152921504606846975" assert=
    most-negative-fixnum 2 + fixnum>string "-1152921504606846974" assert=
    most-positive-fixnum fixnum>string "1152921504606846975" assert=
;

fixnum>string-test-1

test: fixnum>hex-test-1 ( -- )
    -17 fixnum>hex "-11" assert=
      0 fixnum>hex   "0" assert=
     42 fixnum>hex  "2a" assert=
    tag-bits 3 assert=
    most-negative-fixnum     fixnum>hex "-1000000000000000" assert=
    most-negative-fixnum 1 + fixnum>hex  "-fffffffffffffff" assert=
    most-negative-fixnum 2 + fixnum>hex  "-ffffffffffffffe" assert=
    most-positive-fixnum     fixnum>hex   "fffffffffffffff" assert=

    -576460752303423488 >hex "-800000000000000" assert=
     576460752303423487 >hex  "7ffffffffffffff" assert=
;

fixnum>hex-test-1

test: number>string-test-1 ( -- )
    -17 number>string "-17" assert=
    0 number>string "0" assert=
    42 number>string "42" assert=
    tag-bits 3 assert=
    most-negative-fixnum number>string "-1152921504606846976" assert=
    most-positive-fixnum number>string "1152921504606846975" assert=
;

number>string-test-1

test: fixnum-max-test-1 ( -- )
    1 2 fixnum-max 2 eq? assert-true
    2 1 fixnum-max 2 eq? assert-true
    -1 2 fixnum-max 2 eq? assert-true
    2 -1 fixnum-max 2 eq? assert-true
    most-positive-fixnum 0 fixnum-max most-positive-fixnum eq? assert-true
    most-negative-fixnum 0 fixnum-max zero? assert-true
    0 most-positive-fixnum fixnum-max most-positive-fixnum eq? assert-true
    0 most-negative-fixnum fixnum-max zero? assert-true
;

fixnum-max-test-1

test: max-test-1 ( -- )
    1 2 max 2 eq? assert-true
    2 1 max 2 eq? assert-true
    -1 2 max 2 eq? assert-true
    2 -1 max 2 eq? assert-true
    most-positive-fixnum 0 max most-positive-fixnum eq? assert-true
    most-negative-fixnum 0 max zero? assert-true
    0 most-positive-fixnum max most-positive-fixnum eq? assert-true
    0 most-negative-fixnum max zero? assert-true
;

max-test-1

test: fixnum-min-test-1 ( -- )
    1 2 fixnum-min 1 eq? assert-true
    2 1 fixnum-min 1 eq? assert-true
    -1 2 fixnum-min -1 eq? assert-true
    2 -1 fixnum-min -1 eq? assert-true
    most-positive-fixnum 0 fixnum-min zero? assert-true
    most-negative-fixnum 0 fixnum-min most-negative-fixnum eq? assert-true
    0 most-positive-fixnum fixnum-min zero? assert-true
    0 most-negative-fixnum fixnum-min most-negative-fixnum eq? assert-true
;

fixnum-min-test-1

test: min-test-1 ( -- )
    1 2 min 1 eq? assert-true
    2 1 min 1 eq? assert-true
    -1 2 min -1 eq? assert-true
    2 -1 min -1 eq? assert-true
    most-positive-fixnum 0 min zero? assert-true
    most-negative-fixnum 0 min most-negative-fixnum eq? assert-true
    0 most-positive-fixnum min zero? assert-true
    0 most-negative-fixnum min most-negative-fixnum eq? assert-true
;

min-test-1

test: dip-test-1 ( -- )
    87 42 [ 3 * ] dip get-datastack V{ 261 42 } sequence= assert-true
    clear
;

dip-test-1

test: 2dip-test-1 ( -- )
    clear
    42 17 89 [ 1 + ] 2dip get-datastack { 43 17 89 } sequence= assert-true
    clear
;

2dip-test-1

test: each-index-test-1 ( -- )
    clear
    { "a" "b" "c" } [ 2array ] each-index get-datastack
    { { "a" 0 } { "b" 1 } { "c" 2 } } sequence= assert-true
    clear
;

each-index-test-1

test: map-index-test-1 ( -- )
    clear
    { "a" "b" "c" } [ 2array ] map-index get-datastack
    { { { "a" 0 } { "b" 1 } { "c" 2 } } } assert=
    clear
    V{ "a" "b" "c" } [ 2array ] map-index get-datastack
    { V{ { "a" 0 } { "b" 1 } { "c" 2 } } } assert=
    clear
;

map-index-test-1

test: keep-test-1 ( -- )
    42 [ dup ] keep get-datastack V{ 42 42 42 } sequence= assert-true
    clear
;

keep-test-1

test: bi@-test-1
    3 4 [ dup ] bi@ get-datastack V{ 3 3 4 4 } sequence= assert-true
    clear
;

bi@-test-1

test: array-test-1 ( -- )
    10 42 <array>
    [ object? assert-true ] keep
    [ array? assert-true ] keep
    [ length 10 assert= ] keep
    10 [ over nth 42 eq? assert-true ] each-integer
    drop
;

array-test-1

test: array-test-2 ( -- )
    10 f <array>
    10 [ ( array index ) [ number>string ] keep pick set-nth ] each-integer
    drop
;

array-test-2

test: 1array-test-1 ( -- )
    clear
    71 1array get-datastack { { 71 } } sequence= assert-true
    clear
;

1array-test-1

test: 2array-test-1 ( -- )
    clear
    "test1" "test2" 2array get-datastack { { "test1" "test2" } } sequence= assert-true
    clear
;

2array-test-1

test: 3array-test-1 ( -- )
    clear
    1 2 3 3array get-datastack { { 1 2 3 } } sequence= assert-true
    clear
;

3array-test-1

test: 4array-test-1 ( -- )
    clear
    1 2 3 4 4array get-datastack { { 1 2 3 4 } } sequence= assert-true
    clear
;

4array-test-1

-- vector-insert-nth!
test: vector-test-1 ( -- )
    17 0 V{ 0 1 2 3 } [ vector-insert-nth! ] keep
    V{ 17 0 1 2 3 } sequence= assert-true
    clear
;

vector-test-1

test: vector-test-2 ( -- )
    1 <vector> [ vector? assert-true ] keep
    100 [ dup pick vector-set-nth ] each-integer
    [ [ eq? assert-true ] vector-each-index ] keep
    clear

    1 <vector>
    "test" [ 100 pick vector-set-nth ] keep
    100 pick vector-nth eq? assert-true

    -- verify that new cells are initialized to f
    100 [ over vector-nth assert-false ] each-integer
    clear

    local: v
    1 <vector> !> v
    42 9 v set-nth
    87 v push
    v V{ f f f f f f f f f 42 87 } sequence= assert-true

    -- FIXME this fails Nov 25 2016 9:42 AM
    -- 14 19 v set-nth
    -- v V{ f f f f f f f f f 42 87 f f f f f f f f 14 } sequence= assert-true
;

vector-test-2

test: vector-test-3 ( -- )
    1 <vector> [ vector? assert-true ] keep
    10 [ over vector-push ] each-integer
    10 [ dup pick vector-nth eq? assert-true ] each-integer
    [ [ eq? assert-true ] each-index ] keep
    10 [ 9 swap - over vector-pop eq? assert-true ] each-integer
    vector-length zero? assert-true
;

vector-test-3

-- vector-pop*
test: vector-test-4 ( -- )
    V{ 1 2 3 } [ vector-pop* ] keep
    [ length 2 assert= ] keep
    [ vector-pop* ] keep
    [ length 1 assert= ] keep
    [ vector-pop* ] keep
    [ length zero? assert-true ]
;

vector-test-4

-- vector-last
test: vector-test-5 ( -- )
    clear
    V{ 1 2 3 } vector-last get-datastack { 3 } sequence= assert-true
    clear
    V{ "test" } vector-last "test" assert=
;

vector-test-5

-- vector>array
test: vector>array-test-1 ( -- )
    V{ 1 2 3 } vector>array
    [ array? assert-true ] keep
    { 1 2 3 } assert=
;

vector>array-test-1

: %local-test-1 ( -- 42 )
    local: foo
    42 !> foo
    foo
;

test: local-test-1 ( -- )
    %local-test-1 42 eq? assert-true
;

local-test-1

: %local-test-2 ( -- )
    42 >local: foo
    17 >local: bar
    foo bar -
;

test: local-test-2 ( -- )
    %local-test-2 25 eq? assert-true
;

local-test-2

test: if-test-1 ( -- )
    t [ 42 ] [ 17 ] if 42 eq? assert-true
    f [ 42 ] [ 17 ] if 17 eq? assert-true
    0 [ 42 ] [ 17 ] if 42 eq? assert-true
;

if-test-1

test: if*-test-1 ( -- )
    clear
    t [ 42 ] [ 17 ] if* get-datastack { t 42 } sequence= assert-true
    clear
    f [ 42 ] [ 17 ] if* get-datastack { 17 } sequence= assert-true
    clear
    0 [ 42 ] [ 17 ] if* get-datastack { 0 42 } sequence= assert-true
    clear
;

if*-test-1

test: symbol?-test-1 ( -- )
    \ dup symbol? assert-true
    \ assert-true symbol? assert-true
    42 symbol? assert-false
    -3 symbol? assert-false
    "test" symbol? assert-false
;

symbol?-test-1

test: string>number-test-1 ( -- )
    "." string>number assert-false
    "-" string>number assert-false
;

string>number-test-1

test: string>number-test-2 ( -- )
    "%11" string>number 3 eq? assert-true
    "%1100" string>number 12 eq? assert-true
;

string>number-test-2

test: \-test-1 ( -- )
    [ \ dup ] call symbol? assert-true
    3 [ dup ] call get-datastack { 3 3 } sequence= assert-true
    clear
;

\-test-1

test: hashcode-test-1 ( -- )
    "test"
    [ string-hashcode fixnum? assert-true ] keep
    [ hashcode dup fixnum? assert-true ] keep
    string-hashcode eq? assert-true
;

hashcode-test-1

test: hashcode-test-2 ( -- )
    \ dup
    [ symbol-name "dup" string= assert-true ] keep
    [ symbol-vocab-name "feline" string= assert-true ] keep
    [ symbol-hashcode ] keep
    [ symbol-name hashcode ] keep
    symbol-vocab-name hashcode hash-combine eq? assert-true
;

hashcode-test-2

test: hashtable-test-1 ( -- )
    local: ht
    16 <hashtable> !> ht
    42 "parker" ht set-at
    ht hashtable-count 1 assert=
    87 "penelope" ht set-at
    ht hashtable-count 2 assert=
    "parker" ht at* assert-true
    42 assert=
    "penelope" ht at* assert-true
    87 assert=
    "cody may" ht at* assert-false
    assert-false
;

hashtable-test-1

test: hashtable-test-2 ( -- )
    72 <hashtable> hashtable-capacity 128 assert=
;

hashtable-test-2

test: hashtable-test-3 ( -- )
    local: ht
    16 <hashtable> !> ht
    1000 [ dup number>string ht set-at ] each-integer
    1000 [ dup number>string ht at assert= ] each-integer
    "charlie" ht at* assert-false assert-false
;

hashtable-test-3

global: ht

constant: n1    100     -- total number of entries
constant: n2     40     -- number of entries to delete

test: hashtable-test-4 ( -- )
    1 <hashtable> !> ht
    n1 [ dup number>string ht set-at ] each-integer
    n1 [ dup number>string ht at assert= ] each-integer

    -- delete first n2 entries
    n2 [ number>string ht delete-at ] each-integer

    -- verify remaining entries are findable
    n1 n2 - [ n2 + dup number>string ht at assert= ] each-integer

    -- delete first n2 entries again (should make no difference)
    n2 [ number>string ht delete-at ] each-integer

    -- verify remaining entries are findable
    n1 n2 - [ n2 + dup number>string ht at assert= ] each-integer

    -- add deleted entries back
    n2 [ dup number>string ht set-at ] each-integer

    -- verify all entries are findable
    n1 [ dup number>string ht at assert= ] each-integer

    ht hashtable-keys length n1 eq? assert-true
    ht hashtable-keys [ string? assert-true ] each
    ht hashtable-values length n1 eq? assert-true
    ht hashtable-values [ fixnum? assert-true ] each

    f !> ht
;

hashtable-test-4

test: length-test-1 ( -- )
    "test" length 4 eq? assert-true
    "testing" string>sbuf length 7 eq? assert-true
    { 1 2 3 } length 3 eq? assert-true
    V{ 1 2 3 "test" } length 4 eq? assert-true
;

length-test-1

test: globals-test-1 ( -- )
    clear
    42 \ dup set-global get-datastack { } sequence= assert-true
    \ dup get-global get-datastack { 42 } sequence= assert-true
    clear
    \ dup get get-datastack { 42 } sequence= assert-true
    clear
;

globals-test-1

test: find-test-1 ( -- )
    V{ 1 2 3 4 5 6 } [ 3 = ] find
    get-datastack V{ 2 3 } sequence= assert-true
    clear
    V{ 1 2 3 4 5 6 } [ 17 = ] find
    get-datastack f f 2array sequence= assert-true
    clear
    4 V{ 1 2 3 4 5 6 } [ dupd = ] find
    get-datastack V{ 4 3 4 } sequence= assert-true
    clear
;

find-test-1

test: find-from-test-1 ( -- )
    4 "this is a test" [ 'e' = ] find-from
    get-datastack { 11 'e' } sequence= assert-true
    clear
    0 "this is a test" [ 's' = ] find-from
    get-datastack { 3 's' } sequence= assert-true
    clear
    99 "this is a test" [ 'e' = ] find-from
    get-datastack { f f } sequence= assert-true
    clear
    -1 "this is a test" [ 'e' = ] find-from
    get-datastack { f f } sequence= assert-true
    clear
;

find-from-test-1

test: find-last-from-test-1
    13 "this is a test" [ 'e' = ] find-last-from
    get-datastack { 11 'e' } sequence= assert-true
    clear
    99 "this is a test" [ 'e' = ] find-last-from
    get-datastack { f f } sequence= assert-true
    clear
    -99 "this is a test" [ 'e' = ] find-last-from
    get-datastack { f f } sequence= assert-true
    clear
    4 V{ 1 2 3 4 5 3 } [ 3 = ] find-last-from
    get-datastack { 2 3 } sequence= assert-true
    clear
    3 4 V{ 1 2 3 4 5 3 } [ dupd = ] find-last-from
    get-datastack { 3 2 3 } sequence= assert-true
    clear
;

find-last-from-test-1

test: map-find-test-1 ( -- )
    clear
    V{ 1 2 3 4 5 6 } [ 3 = ] map-find get-datastack { t 3 } sequence= assert-true
    clear
    V{ 1 2 3 4 5 6 } [ 17 = ] map-find get-datastack { f f } sequence= assert-true
    clear
    4 V{ 1 2 3 4 5 6 } [ dupd = ] map-find get-datastack { 4 t 4 } sequence= assert-true
    clear
;

map-find-test-1

test: find-integer-test-1 ( -- )
    clear
    10 [ 5 eq? ] find-integer get-datastack { 5 } assert=
    clear
;

find-integer-test-1

test: ?lookup-symbol-test-1 ( -- )
    "dup" "feline" ?lookup-symbol \ dup eq? assert-true
    "xxx" "feline" ?lookup-symbol assert-false
    "dup" "xxx" ?lookup-symbol assert-false
;

?lookup-symbol-test-1

test: create-symbol-test-1 ( -- )
    "effluvia" "test" ?lookup-symbol assert-false
    "effluvia" "test" create-symbol
    dup symbol? assert-true
    dup "effluvia" "test" ?lookup-symbol eq? assert-true
    symbol-vocab-name "test" assert=
;

create-symbol-test-1

test: immediate?-test-1 ( -- )
    \ symbol: immediate? assert-true
    \ dup immediate? assert-false
    42 immediate? assert-false
    f immediate? assert-false
    \ f immediate? assert-true
;

immediate?-test-1

test: parsing-test-1 ( -- )
    V{ symbol: this-is-a-new-symbol } V{ } sequence= assert-true
    clear
;

parsing-test-1

: %parsing-test-2 ( -- )
    symbol: another-new-symbol
    42
;

test: parsing-test-2 ( -- )
    clear
    %parsing-test-2 get-datastack { 42 } sequence= assert-true
    clear
;

parsing-test-2

test: sequence=-test-1 ( -- )
    { "this is a test" } { "this is a test" } sequence= assert-true
;

sequence=-test-1

test: symbol-props-test-1 ( -- )
    clear
    "foo" \ dup symbol-prop get-datastack { f } sequence= assert-true
    clear
    "this is a test" "foo" \ dup symbol-set-prop
    "foo" \ dup symbol-prop get-datastack { "this is a test" } sequence= assert-true
    clear

    -- clean up
    f "foo" \ dup symbol-set-prop
;

symbol-props-test-1

test: symbol-props-gc-test-1 ( -- )
    "this is a test" "foo" \ dup symbol-set-prop
    "foo" \ dup symbol-prop get-datastack { "this is a test" } sequence= assert-true
    clear

    gc

    "foo" \ dup symbol-prop get-datastack { "this is a test" } sequence= assert-true
    clear

    -- clean up
    f "foo" \ dup symbol-set-prop
;

symbol-props-gc-test-1

test: times-test-1 ( -- )
    0 10 [ 1 + ] times 10 eq? assert-true
;

times-test-1

test: filter-test-1 ( -- )
    V{ 1 2 3 4 } [ 3 < ] filter V{ 1 2 } sequence= assert-true
     { 1 2 3 4 } [ 3 < ] filter  { 1 2 } sequence= assert-true
;

filter-test-1

test: in-bounds?-test-1 ( -- )
    { 1 2 3 }
    0 over in-bounds? assert-true
    1 over in-bounds? assert-true
    2 over in-bounds? assert-true
    3 over in-bounds? assert-false
    -1 swap in-bounds? assert-false
;

in-bounds?-test-1

: %foo ( -- ) 42 ;

: %bar ( -- ) %foo ;

test: redefinition-test-1 ( -- )
    %foo 42 assert=
    %bar 42 assert=
;

redefinition-test-1

-- redefine %foo
: %foo ( -- ) 87 ;

test: redefinition-test-2 ( -- )
    %foo 87 assert=
    %bar 87 assert=
;

redefinition-test-2

test: until-test-1 ( -- )
    clear
    1 [ dup 1 = ] [ 1 + ] until get-datastack { 1 } sequence= assert-true
    clear
    1 [ dup 4 = ] [ 1 + ] until get-datastack { 4 } sequence= assert-true
    clear
;

until-test-1

test: ensure-symbol-test-1 ( -- )
    "foo" "feline" ensure-symbol dup symbol? assert-true
    dup "foo" "feline" ?lookup-symbol eq? assert-true
    "foo" "feline" ensure-symbol eq? assert-true
    "foo" "feline" ensure-symbol symbol-vocab-name "feline" assert=
    \ dup "dup" "feline" ensure-symbol eq? assert-true
;

ensure-symbol-test-1

test: bi-test-1 ( -- )
    clear
    42 [ 1 + ] [ 2 + ] bi get-datastack { 43 44 } sequence= assert-true
    clear
;

bi-test-1

test: 2bi-test-1
    clear
    1 2 3 4 [ dup ] [ + ] 2bi get-datastack { 1 2 3 4 4 7 } sequence= assert-true
    clear
    1 2 3 4 [ nip ] [ + ] 2bi get-datastack { 1 2 4 7 } sequence= assert-true
    clear
;

2bi-test-1

global: temp-string "test" !> temp-string

TUPLE: foo a b c ;

test: tuple-test-1 ( -- )
    \ foo new
    [ tuple? assert-true ] keep
    [ foo? assert-true ] keep
    \ foo require-type
    [ tuple-size 3 assert= ] keep
    [ foo-a assert-false ] keep
    [ foo-b assert-false ] keep
    [ foo-c assert-false ] keep
    42 over foo-a!
    71 over foo-b!
    temp-string over foo-c!
    [ foo-a 42 assert= ] keep
    [ foo-b 71 assert= ] keep
    [ foo-c temp-string assert= ] keep
    [ foo-c "test" assert= ] keep

    f !> temp-string
    gc
    [ foo-c "test" assert= ] keep

    drop
;

tuple-test-1

test: vocab?-test-1 ( -- )
    42 vocab? assert-false
    "feline" vocab? assert-false
    "feline" lookup-vocab vocab? assert-true
;

vocab?-test-1

test: lookup-vocab-test-1 ( -- )
    "feline" lookup-vocab vocab? assert-true
    "feline" lookup-vocab ( vocab ) lookup-vocab vocab? assert-true
    "feline" lookup-vocab ( vocab ) dup lookup-vocab eq? assert-true
    current-vocab lookup-vocab vocab? assert-true
    "not a vocab" lookup-vocab vocab? assert-false
;

lookup-vocab-test-1

test: unless*-test-1 ( -- )
    clear
    f [ 42 ] unless* get-datastack { 42 } assert=
    clear
    t [ 42 ] unless* get-datastack { t } assert=
    clear
    "test" [ 42 ] unless* get-datastack { "test" } assert=
    clear
;

unless*-test-1

global: zork

test: global-test-1 ( -- )
    42 !> zork zork 42 eq? assert-true
;

global-test-1

-- : global-test-2 ( -- )
--     << last-word symbol-name >> ?nl write-string
--     87 \ zork set-global
--     \ zork get-global 87 eq? assert-true

--     [
--         "test" dup zork! zork eq? assert-true
--         \ zork get-global 87 eq? assert-true
--         19 \ zork set-global
--         zork "test" assert=
--         \ zork get-global 19 eq? assert-true
--     ] with-scope

--     \ zork get-global 19 eq? assert-true
-- ;

-- global-test-2

-- : global-test-3 ( -- )
--     << last-word symbol-name >> ?nl write-string
--     f zork!
--     zork assert-false
--     [ [ 71 zork! zork ] with-scope ] [ ] recover 71 eq? assert-true
--     [ [ 71 zork! "error" error ] with-scope ] [ drop zork ] recover assert-false
-- ;

-- global-test-3

test: index-test-1 ( -- )
    3 { 1 2 3 4 } index 2 eq? assert-true
    99 { 1 2 3 4 } index assert-false
    's' "this is a test" index 3 eq? assert-true
    'a' "this is a test" index 8 eq? assert-true
    "bar" { "foo" "bar" "baz" } index 1 eq? assert-true
;

index-test-1

test: member?-test-1 ( -- )
    17 { 1 2 3 } member? assert-false
    1  { 1 2 3 } member? assert-true
    f  { 1 2 3 } member? assert-false
    f  { 1 f 3 } member? assert-true
;

member?-test-1

test: member?-test-2 ( -- )
    "bar" { "foo" "bar" "baz" } member? assert-true
    "bag" { "foo" "bar" "baz" } member? assert-false
    4 { 1 2 3 4 } member? assert-true
    1 { 1 2 3 4 } member? assert-true
    "test" { 1 2 3 4 } member? assert-false
    0 { 1 2 3 4 } member? assert-false
;

member?-test-2

test: string-head-test-1 ( -- )
    "this is a test"
    [ 4 string-head "this" assert= ] keep

    -- FIXME "" confuses the parser Aug 22 2016 12:49 PM
--     [ 0 string-head "" assert= ] keep

    [ 0 string-head [ string? assert-true ] [ length zero? assert-true ] bi ] keep
    [ 14 string-head "this is a test" assert= ] keep
    drop
;

string-head-test-1

test: string-tail-test-1 ( -- )
    "this is a test"
    [ 10 string-tail "test" assert= ] keep
    [ 14 string-tail [ string? assert-true ] [ length zero? assert-true ] bi ] keep
    [ 0 string-tail "this is a test" assert= ] keep
    drop
;

string-tail-test-1

constant: magenta 5

test: constant-test-1 ( -- )
    magenta 5 eq? assert-true
;

constant-test-1

test: string-skip-whitespace-test-1 ( -- )
    "   this is a test   "
    [  0 swap string-skip-whitespace  3 eq? assert-true ] keep
    [  2 swap string-skip-whitespace  3 eq? assert-true ] keep
    [  3 swap string-skip-whitespace  3 eq? assert-true ] keep
    [  6 swap string-skip-whitespace  6 eq? assert-true ] keep
    [  7 swap string-skip-whitespace  8 eq? assert-true ] keep
    [ 16 swap string-skip-whitespace 16 eq? assert-true ] keep
    [ 17 swap string-skip-whitespace  f eq? assert-true ] keep
    [ 18 swap string-skip-whitespace  f eq? assert-true ] keep
    [ 19 swap string-skip-whitespace  f eq? assert-true ] keep
    [ 20 swap string-skip-whitespace  f eq? assert-true ] keep
    [ 99 swap string-skip-whitespace  f eq? assert-true ] keep
    drop
;

string-skip-whitespace-test-1

-- multiline string
test: string-skip-whitespace-test-2 ( -- )
    "   this is
a multiline string   "
    [  0 swap string-skip-whitespace  3 eq? assert-true ] keep
    [  2 swap string-skip-whitespace  3 eq? assert-true ] keep
    [  3 swap string-skip-whitespace  3 eq? assert-true ] keep
    [  6 swap string-skip-whitespace  6 eq? assert-true ] keep
    [  7 swap string-skip-whitespace  8 eq? assert-true ] keep
    [ 10 swap string-skip-whitespace 11 eq? assert-true ] keep
    [ 11 swap string-skip-whitespace 11 eq? assert-true ] keep
    [ 12 swap string-skip-whitespace 13 eq? assert-true ] keep
    drop
;

string-skip-whitespace-test-2

test: string-skip-to-whitespace-test-1 ( -- )
    "   this is a test   "
    [  0 swap string-skip-to-whitespace  0 eq? assert-true ] keep
    [  2 swap string-skip-to-whitespace  2 eq? assert-true ] keep
    [  3 swap string-skip-to-whitespace  7 eq? assert-true ] keep
    [  6 swap string-skip-to-whitespace  7 eq? assert-true ] keep
    [  7 swap string-skip-to-whitespace  7 eq? assert-true ] keep
    [  8 swap string-skip-to-whitespace 10 eq? assert-true ] keep
    [ 16 swap string-skip-to-whitespace 17 eq? assert-true ] keep
    [ 17 swap string-skip-to-whitespace 17 eq? assert-true ] keep
    [ 18 swap string-skip-to-whitespace 18 eq? assert-true ] keep
    [ 19 swap string-skip-to-whitespace 19 eq? assert-true ] keep
    [ 20 swap string-skip-to-whitespace  f eq? assert-true ] keep
    [ 99 swap string-skip-to-whitespace  f eq? assert-true ] keep
    drop
;

string-skip-to-whitespace-test-1

-- multiline string
test: string-skip-to-whitespace-test-2 ( -- )
    "   this is
a multiline string   "
    [  3 swap string-skip-to-whitespace  7 eq? assert-true ] keep
    [  7 swap string-skip-to-whitespace  7 eq? assert-true ] keep
    [  8 swap string-skip-to-whitespace 10 eq? assert-true ] keep
    [ 10 swap string-skip-to-whitespace 10 eq? assert-true ] keep
    [ 11 swap string-skip-to-whitespace 12 eq? assert-true ] keep
    drop
;

string-skip-to-whitespace-test-2

test: ?-test-1 ( -- )
    t "yes" "no" ? "yes" assert=
    f "yes" "no" ? "no"  assert=
    0 "yes" "no" ? "yes" assert=
;

?-test-1

test: slice-test-1
    2 5 { 11 12 13 14 15 16 17 17 } <slice>    { 13 14 15 } sequence= assert-true
    2 5 { 11 12 13 14 15 16 17 17 } <slice> gc { 13 14 15 } sequence= assert-true

    2 5 { 11 12 13 14 15 16 17 17 } <slice> dup slice? assert-true

    [ slice-length      3 eq? assert-true ] keep
    [ 0 swap slice-nth 13 eq? assert-true ] keep
    [ 1 swap slice-nth 14 eq? assert-true ] keep
    [ 2 swap slice-nth 15 eq? assert-true ] keep

    [ 0 swap slice-nth-unsafe 13 eq? assert-true ] keep
    [ 1 swap slice-nth-unsafe 14 eq? assert-true ] keep
    [ 2 swap slice-nth-unsafe 15 eq? assert-true ] keep

    [ length      3 eq? assert-true ] keep
    [ 0 swap nth 13 eq? assert-true ] keep
    [ 1 swap nth 14 eq? assert-true ] keep
    [ 2 swap nth 15 eq? assert-true ] keep

    drop
;

slice-test-1

test: index?-test-1
    0 index? assert-true
    1 index? assert-true
    most-positive-fixnum index? assert-true
    -1 index? assert-false
    most-negative-fixnum index? assert-false
    "test" index? assert-false
    { } index? assert-false
;

index?-test-1

test: bounds-check?-test-1
    4 { 0 1 2 3 } [ bounds-check? ] curry each-integer
    get-datastack { t t t t } sequence= assert-true
    clear

    { 0 1 2 3 }
    -1 over bounds-check? assert-false
    4 over bounds-check? assert-false
    42  over bounds-check? assert-false
    drop
;

bounds-check?-test-1

test: ?nth-test-1
    4 { 0 1 2 3 } [ ?nth ] curry each-integer
    get-datastack { 0 1 2 3 } sequence= assert-true
    clear

    { 0 1 2 3 }
    -1 over ?nth assert-false
    4 over ?nth assert-false
    42  over ?nth assert-false
    drop
;

?nth-test-1

test: ?nth-test-2
    4 V{ 0 1 2 3 } [ ?nth ] curry each-integer
    get-datastack { 0 1 2 3 } sequence= assert-true
    clear

    V{ 0 1 2 3 }
    -1 over ?nth assert-false
    4 over ?nth assert-false
    42  over ?nth assert-false
    drop
;

?nth-test-2

test: and-test-1
    t f and assert-false
    f t and assert-false
    0 t and assert-true
    t 0 and 0 eq? assert-true

    f 71 and 71 eq? assert-false
    t 71 and 71 eq? assert-true
    "test" 42 and 42 eq? assert-true
;

and-test-1

test: or-test-1
    t f or assert-true
    f t or assert-true
    0 t or 0 eq? assert-true
    t 0 or assert-true

    f 71 or 71 eq? assert-true
    t 71 or assert-true
    "test" 42 or "test" assert=
;

or-test-1

test: sbuf-insert-nth!-test-1
    "oat" string>sbuf >local: sb
    'b' 0 sb sbuf-insert-nth!
    sb "boat" sequence= assert-true
    's' 3 sb sbuf-insert-nth!
    sb "boast" sequence= assert-true
;

sbuf-insert-nth!-test-1

test: sbuf-?last-test-1
    "testing" string>sbuf sbuf-?last 'g' eq? assert-true
    16 <sbuf> sbuf-?last assert-false
;

sbuf-?last-test-1

test: sbuf-reverse!-test-1
    "12345678"  string>sbuf sbuf-reverse!  "87654321" sequence= assert-true
    "123456789" string>sbuf sbuf-reverse! "987654321" sequence= assert-true
    "1" string>sbuf sbuf-reverse! "1" sequence= assert-true
    "" string>sbuf sbuf-reverse! "" sequence= assert-true
;

sbuf-reverse!-test-1

test: string-index-from-test-1
    't' 0 "this is a test" string-index-from zero? assert-true
    'h' 0 "this is a test" string-index-from 1 eq? assert-true
    'h' 2 "this is a test" string-index-from assert-false
    't' 1 "this is a test" string-index-from 10 eq? assert-true
    'e' 4 "this is a test" string-index-from 11 eq? assert-true
    'x' 0 "this is a test" string-index-from assert-false
;

string-index-from-test-1

test: fixnum>bignum-test-1
     0 fixnum>bignum bignum>string  "0" assert=
     1 fixnum>bignum bignum>string  "1" assert=
    -1 fixnum>bignum bignum>string "-1" assert=
    42 fixnum>bignum bignum>string "42" assert=
    most-positive-fixnum fixnum>bignum bignum>string
    most-positive-fixnum fixnum>string assert=
;

fixnum>bignum-test-1

test: string>integer-test-1
    most-positive-fixnum fixnum>string string>integer most-positive-fixnum assert=
    most-negative-fixnum fixnum>string string>integer most-negative-fixnum assert=
;

string>integer-test-1

test: fixnum-fixnum+-test-1
    1  2 fixnum-fixnum+  3 eq? assert-true
    1 -2 fixnum-fixnum+ -1 eq? assert-true

    most-positive-fixnum   0 fixnum-fixnum+ most-positive-fixnum eq? assert-true
    most-positive-fixnum   1 fixnum-fixnum+ bignum>string  "1152921504606846976" assert=
    most-negative-fixnum  -1 fixnum-fixnum+ bignum>string "-1152921504606846977" assert=
    most-positive-fixnum dup fixnum-fixnum+ bignum>string  "2305843009213693950" assert=
    most-negative-fixnum dup fixnum-fixnum+ bignum>string "-2305843009213693952" assert=
;

fixnum-fixnum+-test-1

test: negate-test-1
    0 negate zero? assert-true
    42 negate -42 eq? assert-true
    most-positive-fixnum negate number>string "-1152921504606846975" assert=
    most-positive-fixnum dup fixnum-fixnum+ negate number>string  "-2305843009213693950" assert=
    most-negative-fixnum negate number>string "1152921504606846976" assert=
    most-negative-fixnum dup fixnum-fixnum+ negate number>string "2305843009213693952" assert=
;

negate-test-1

test: expt-test-1
    2 64 expt 18446744073709551616 assert=
    2 128 expt 340282366920938463463374607431768211456 assert=
    7 17 expt 232630513987207 assert=
    17 50 expt 33300140732146818380750772381422989832214186835186851059977249 assert=
;

expt-test-1

test: string-substring-test-1
    "this is a test"
     0  4 pick string-substring "this"  assert=
    10 14 pick string-substring "test"  assert=
     5 10 pick string-substring "is a " assert=
    13 14 pick string-substring "t"     assert=

    0 0 pick string-substring "" assert=
    4 4 pick string-substring "" assert=

    0 14 rot string-substring "this is a test" assert=
;

string-substring-test-1

test: sbuf-substring-test-1
    "this is a test" string>sbuf
     0  4 pick sbuf-substring "this"  assert=
    10 14 pick sbuf-substring "test"  assert=
     5 10 pick sbuf-substring "is a " assert=
    13 14 pick sbuf-substring "t"     assert=

    0 0 pick sbuf-substring "" assert=
    4 4 pick sbuf-substring "" assert=

    0 14 rot sbuf-substring "this is a test" assert=
;

sbuf-substring-test-1

-- generic
test: substring-test-1
    "this is a test"
     0  4 pick string-substring "this"  assert=
    10 14 pick string-substring "test"  assert=
     5 10 pick string-substring "is a " assert=
    13 14 pick string-substring "t"     assert=

    0 0 pick string-substring "" assert=
    4 4 pick string-substring "" assert=

    0 14 pick string-substring "this is a test" assert=

    string>sbuf
     0  4 pick sbuf-substring "this"  assert=
    10 14 pick sbuf-substring "test"  assert=
     5 10 pick sbuf-substring "is a " assert=
    13 14 pick sbuf-substring "t"     assert=

    0 0 pick sbuf-substring "" assert=
    4 4 pick sbuf-substring "" assert=

    0 14 rot sbuf-substring "this is a test" assert=
;

substring-test-1

test: vector-find-string-test-1 ( -- )
    V{ "foo" "bar" "baz" "zork" } >local: v
    "foo"  v vector-find-string assert-true 0 assert=
    "zork" v vector-find-string assert-true 3 assert=
    "baz"  v vector-find-string assert-true 2 assert=
    "bar"  v vector-find-string assert-true 1 assert=

    "cat"  v vector-find-string assert-false "cat" assert=
;

vector-find-string-test-1

test: 1+!>-test-1 ( -- )
    42 >local: bar
    bar 42 assert=
    1+!> bar
    bar 43 assert=
    0 >local: foo
    100 [ foo assert= 1+!> foo ] each-integer
;

1+!>-test-1

test: 1-!>-test-1 ( -- )
    42 >local: bar
    bar 42 assert=
    1-!> bar
    bar 41 assert=
    0 >local: foo
    100 [ negate foo assert= 1-!> foo ] each-integer
;

1-!>-test-1

global: bar

test: 1+!>-test-2 ( -- )
    \ bar symbol-global? assert-true
    42 !> bar
    bar 42 assert=
    1+!> bar
    bar 43 assert=
    0 !> bar
    100 [ bar assert= 1+!> bar ] each-integer
;

1+!>-test-2

test: 1-!>-test-2 ( -- )
    \ bar symbol-global? assert-true
    42 !> bar
    bar 42 assert=
    1-!> bar
    bar 41 assert=
    0 !> bar
    100 [ negate bar assert= 1-!> bar ] each-integer
;

1-!>-test-2

test: 1+!>-test-3 ( -- )
    \ bar symbol-global? assert-true
    \ test:bar \ bar eq? assert

    42 !> bar
    bar 42 assert=
    1+!> bar
    bar 43 assert=

    87 >local: bar
    bar 87 assert=

    test:bar 43 assert=

    1+!> bar
    bar 88 assert=
    0 !> bar
    100 [ bar assert= 1+!> bar ] each-integer

    test:bar 43 assert=
;

1+!>-test-3

in: feline

?nl "Reached end of test.feline" write-string
