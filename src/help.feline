-- Copyright (C) 2016-2017 Peter Graves <gnooth@gmail.com>

-- This program is free software: you can redistribute it and/or modify
-- it under the terms of the GNU General Public License as published by
-- the Free Software Foundation, either version 3 of the License, or
-- (at your option) any later version.

-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
-- GNU General Public License for more details.

-- You should have received a copy of the GNU General Public License
-- along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feline ;
in: help

empty

private

   96 >constant: backquote

white >constant: fg-title
 cyan >constant: fg-parameter
white >constant: fg-default

: bold   ( -- ) color? [ esc[ "1m" write-string ] when ;
: normal ( -- ) color? [ esc[ "0m" write-string ] when ;

: format-string ( string -- )
    >local: s
    color? [
        f >local: in-quote?
        backquote s index >local: i
        [ i ] [
            s i string-head write-string
            1+!> i
            s i string-tail !> s
            in-quote? not !> in-quote?
            in-quote? fg-parameter fg-default ? foreground
            backquote s index !> i
        ] while
    ] when
    s write-string ;

: format-title ( string -- )
    bold
    fg-title foreground
    write-string
    normal
    fg-default foreground ;

feline-source-directory "feline.help" path-append >constant: glossary-file-name

global: glossary
global: glossary-file-write-time

: cache-glossary ( -- )
    glossary [
        glossary-file-name file-write-time glossary-file-write-time eq? [
            f !> glossary
        ] unless
    ] when

    glossary [
        ?nl "Reading glossary..." print
        glossary-file-name [ file-lines !> glossary ] [ file-write-time !> glossary-file-write-time ] bi
    ] unless
;

public

: symbol-help ( symbol -- string/f )
    symbol-name >local: name

    cache-glossary

    glossary [ name swap string-has-prefix? ] find    -- index/f element/f

    >local: line
    >local: line-number

    local: v

    f >local: help-string

    line [
        8 <vector> !> v
        [
            line-number 1 + !> line-number
            line-number glossary vector-length >=
        ] [
            line-number glossary vector-nth ( string )
            dup "    " over string-has-prefix? swap empty? or [
                v vector-push
            ] [
                drop glossary vector-length !> line-number
            ] if
        ] until

        v empty? [
            v vector-last empty? [ v vector-pop* ] when
        ] unless

        256 <sbuf> >local: sb
        v [ sb sbuf-append-string 10 sb sbuf-push ] each
        sb sbuf>string !> help-string
    ] when

    help-string
;

: help ( symbol -- )
    dup symbol-name >local: name
    symbol-help [
        name format-title nl
        format-string
    ] when*
;

using: feline help ;
in: feline

: h ( -- )
    parse-token [ find-name [ help ] [ drop ] if ] when* ;
