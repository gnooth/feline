// Copyright (C) 2016 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

LANGUAGE: feline

USING: feline ;
IN: feline

: tuple-instance? ( object class -- ? )
    over tuple?
    [ swap layout-of first eq? ]
    [ 2drop f ]
    if
;

: define-reader ( class-name slot-name slot-offset )
    rot string>sbuf                     // -- slot-name slot-offset sbuf
    '-' over push                       // -- slot-name slot-offset sbuf
    rot 2dup sbuf-append-string drop    // -- slot-offset sbuf
    sbuf>string current-vocab <symbol>
    dup current-vocab vocab-add-symbol  // -- slot-offset symbol

    [ \ slot@ 2array array>quotation ] dip
    symbol-set-def
;

: define-writer ( class-name slot-name slot-offset )
    rot string>sbuf                     // -- slot-name slot-offset sbuf
    '-' over push                       // -- slot-name slot-offset sbuf
    rot 2dup sbuf-append-string drop    // -- slot-offset sbuf
    '!' over push
    sbuf>string current-vocab <symbol>
    dup current-vocab vocab-add-symbol  // -- slot-offset symbol

    [ \ slot! 2array array>quotation ] dip
    symbol-set-def
;

: define-accessors ( class -- )
    "slots" over symbol-prop            // class slots
    [ symbol-name ] dip                 // class-name slots

    2dup
    [ [ dupd ] dip 2 + define-reader ] each-index
    drop
    [ [ dupd ] dip 2 + define-writer ] each-index
    drop
;

: suffix! ( seq elt -- seq )
    over push
;

: define-class ( class-name -- class )
    current-vocab vocab-create-symbol
    t "class" pick symbol-set-prop
;

: define-tuple-predicate ( class -- )
    dup symbol-name string>sbuf
    '?' over push
    sbuf>string current-vocab <symbol>
    dup current-vocab vocab-add-symbol  // -- class symbol

    swap                                // -- symbol class

    // REVIEW literalize
    <wrapper>

    10 <vector>                         // -- symbol class vector
    [ push ] keep                       // -- symbol vector
    \ tuple-instance? suffix!           // -- symbol vector
    vector>array array>quotation
    swap symbol-set-def
;

: define-tuple-class ( class-name slot-names -- )
    swap define-class                   // -- slot-names class
    "slots"                             // -- slot-names class "slots"
    swap                                // -- slot-names "slots" class

    [ symbol-set-prop ] keep            // -- class

    [ "slots" swap symbol-prop length ] keep // -- size class
    [ swap 2array ] keep                // -- array class
    [ "layout" swap symbol-set-prop ] keep

    [ define-accessors ] keep           // -- class

    define-tuple-predicate
;

: TUPLE: ( -- )
    parse-token                         // -- string/f
    10 <vector>                         // -- string vector

    [ parse-token dup ";" string= ] [ suffix! ] until
    drop
    vector>array
    // -- class-name slot-names
    define-tuple-class
;
