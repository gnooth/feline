-- Copyright (C) 2018 Peter Graves <gnooth@gmail.com>

-- This program is free software: you can redistribute it and/or modify
-- it under the terms of the GNU General Public License as published by
-- the Free Software Foundation, either version 3 of the License, or
-- (at your option) any later version.

-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
-- GNU General Public License for more details.

-- You should have received a copy of the GNU General Public License
-- along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feline ;
in: feline

private

: make-global                           -- string -> symbol
    new-symbol-in-current-vocab
    dup symbol-set-global-bit
    dup <wrapper> ' symbol-value 2array array>quotation over symbol-set-def
    dup compile-word ;

public

syntax: global:
    must-parse-token
    make-global
    drop ;

syntax: >global:
    must-parse-token
    make-global
    symbol-set-value ;

syntax: global
    must-parse-token
    make-global                         -- symbol
    drop ;

private

: make-thread-local-getter              -- string -> symbol
    new-symbol-in-current-vocab
    dup symbol-set-thread-local-bit
    dup <wrapper> ' current-thread-local-get 2array array>quotation over symbol-set-def
    dup compile-word ;

: make-thread-local-setter              -- getter ->
    :> getter
    getter symbol-name "!" string-append new-symbol-in-current-vocab :> setter
    getter <wrapper> ' current-thread-local-set 2array array>quotation setter symbol-set-def
    setter compile-word ;

public

syntax: thread-local:
    must-parse-token
    make-thread-local-getter
    make-thread-local-setter ;

syntax: var
    must-parse-token
    make-thread-local-getter
    make-thread-local-setter ;
