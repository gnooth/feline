// Copyright (C) 2019 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feral-core feline accessors directories mini diff-mode ;
in: editor

: git-diff
    current-buffer filename>> :> source-file

    source-file null? ?exit

    "git diff " source-file + :> command
    command shell-command-to-string !> s
    f !> buf
    s length 0 > [
        buffer-list [ command>> command = ] find // index/f element/f
        [
            nip buf!
        ] [
            drop make-buffer buf!
        ] if*

        s buf buffer-set-contents
        command buf command<<
        "git diff " source-file + buf name<<
        t buf read-only<<
        buf first-line>> 0 make-position buf dot<<
        f buf mark<<
        buf first-line>> buf top-line<<

        initialize-diff-mode buf mode<<
        current-buffer buf parent<<
        buf dup mode>> initialize-buffer

        buf buffer-list member-eq? [
            buf buffer-list vector-push
        ] unless
        buf current-buffer!

        t repaint!
    ] [
        "git ls-files " source-file + shell-command-to-string s!
        s length zero? "Untracked" "No changes" ? message
    ] if ;
