// Copyright (C) 2019 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feral-core feline accessors directories mini diff-mode ;
in: editor

: git-diff
    current-buffer filename>> :> sourcefile
    sourcefile null? ?exit
    get-current-directory :> olddir
    sourcefile file-name-directory set-current-directory
    "git diff " sourcefile + :> command
    command shell-command-to-string !> output
    olddir set-current-directory

    // shell-command-to-string might return f
    output null? ?exit

    f !> buf
    output length 0 > [
        // reuse existing buffer if possible
        buffer-list [ command>> command = ] find // index/f element/f
        [
            nip buf!
        ] [
            drop make-buffer buf!
        ] if*

        output buf buffer-set-contents
        command buf command<<
        command buf name<<
        current-buffer directory>> buf directory<<
        t buf read-only<<
        buf first-line>> 0 make-position buf dot<<
        f buf mark<<
        buf first-line>> buf top-line<<

        current-buffer buf parent<<
        diff-mode:get-instance buf mode<<
        buf mode>> initialize-buffer-function>> [ buf swap call ] when*

        buf buffer-list member-eq? [
            buf buffer-list vector-push
        ] unless
        buf set-current-buffer

        t repaint!
    ] [
        "git ls-files " sourcefile + shell-command-to-string output!
        output length zero? "Untracked" "No changes" ? message
    ] if ;
