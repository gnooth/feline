// Copyright (C) 2018-2020 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feral-core modes feline accessors segments feral-colors ;
in: asm-mode

private

global instance

: get-instance                          // void -> mode
    instance ;

: format-line ( line )                  // line -> segments
    line text>> detab !> s

    8 make-vector :> vec

    s length 0? [ vec ] ?return

    '\s' s string-index s length or !> i
    i 0> [ 0 i s substring ] [ nil ] if :> first-token

    first-token { "%if" "%ifdef" "%else" "%endif" } member? [
        first-token 0 color-preprocessor make-segment/3 vec push
        first-token length s string-tail s!
    ] [
        0 i!
    ] if

    ';' s string-find-char :> j
    j [
        j s string-head i color-text make-segment/3 vec push
        j s string-tail j color-comment make-segment/3 vec push
    ] [
        s i color-text make-segment/3 vec push
    ] if

    vec ;

: line-is-definition? ( line )
    line text>> string-trim-head :> s
    '\s' s string-index :> end
    end nil? [ nil ] ?return
    0 end s string-substring {
        "code" "inline" "always_inline" "subroutine" "%macro" "%define" "deferred"
    } member? ;

: asm-mode-list-symbols
    ' asm-mode:line-is-definition? list-symbols:list-symbols ;

global asm-mode-local-map

feral-config:winui? feral-config:gtkui? or #if

: initialize-local-map
    128 <hashtable> :> m
    ' asm-mode-list-symbols feral-key:ctrl-shift-o m set-at
    m asm-mode-local-map! ;

initialize-local-map

#endif

: asm-mode-initialize-buffer            // buffer -> void
    1 ?enough :> buf
    asm-mode-local-map buf local-map<<
    instance indent-size>> buf indent-size<< ;

: initialize-asm-mode
    mode make-instance :> m
    "Assembly" m name<<
    8 m indent-size<<
    "; " m comment-start<<
    ' format-line m format-line-function<<
    ' asm-mode-initialize-buffer m initialize-buffer-function<<
    ' asm-mode-identifier-char? m identifier-char-function<<
    m ".asm" modes set-at
    m instance! ;

initialize-asm-mode
