// Copyright (C) 2017-2019 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feral-core feline accessors ;
in: editor

: adjust-dot-column
    goal-column dot-offset!
    dot-offset dot-line-length > [
        dot-line-length dot-offset!
    ] when ;

: beginning-of-buffer
    add-undo-move
    unmark
    first-line top-line!
    first-line 0 make-position dot!
    0 goal-column!
    t repaint! ;

: end-of-buffer
    add-undo-move
    unmark
    last-line dot-line!
    dot-line-length dot-offset!
    dot-offset goal-column!

    reframe? [
        dot-line-number textview-rows 1- - nth-line top-line!
        t repaint!
    ] when
;

: gotoline-internal                     // line-number -> void
    1- 0 max line-count 1- min
    dup dot-line-number <> [
        add-undo-move
        unmark
        nth-line dot-line!
        0 dot-offset!
        maybe-reframe
    ] [
        drop
        add-undo-move
        unmark
        0 dot-offset!
    ] if
;

: move-to-bol
    0 !> goal
    dot-line indentation-length :> len

    len zero? [
        dot-offset len neq? [ len goal! ] when
    ] unless

    goal dot-offset neq? [
        add-undo-move
        unmark
        goal dot-offset!
    ] [
        mark [ add-undo-move unmark ] when
    ] if ;

: select-to-bol
    0 !> goal
    dot-line indentation-length :> len

    len zero? [
        dot-offset len neq? [ len goal! ] when
    ] unless

    mark null?
    goal dot-offset neq? or [
        add-undo-move
        mark [ copy-dot mark! ] unless
        goal dot-offset neq? [ goal dot-offset! ] when
        t repaint!
    ] when ;

: move-to-eol
    mark dot-offset dot-line-length neq? or [
        add-undo-move
        unmark
        dot-line-length dot-offset!
        dot-offset goal-column!
    ] when ;

: select-to-eol
    add-undo-move
    unmark
    copy-dot mark!
    dot-line-length dot-offset!
    dot-offset goal-column!
    t dot-line-changed! ;

: beginning-of-block
    dot mark make-region/2 begin dot!
;

: end-of-block
    dot mark make-region/2 end dot!
;

: char-left-internal                    // void -> void
    dot-offset 0 > [
        dot-offset 1- dot-offset!
    ] [
        dot-line-prev [
            dot-line-prev dup length make-position dot!
        ] when
    ] if ;

: char-left-line                        // void -> ?
    dot-offset 0 > [ dot-offset 1- dot-offset! t ] [ f ] if ;

: char-right-internal                   // void -> void
    dot-offset dot-line-length < [
        dot-offset 1+ dot-offset!
    ] [
        dot-line-next [
            dot-line-next 0 make-position dot!
        ] when
    ] if ;

: char-right-line                       // void -> ?
    dot-offset dot-line-length < [ dot-offset 1+ dot-offset! t ] [ f ] if ;

: move-left
    last-command ' move-left eq? [ add-undo-move ] unless

    mark [
        beginning-of-block
        unmark
    ] return-if

    dot-offset 0 > [
        dot-offset 1- dot-offset!
        dot-offset goal-column!
    ] [
        dot-line-prev [
            dot-line-prev dot-line!
            dot-line-length dot-offset!
            dot-offset goal-column!
            reframe? [
                dot-line top-line!
                t repaint!
            ] when
        ] when
    ] if ;

: select-left
    last-command ' select-left eq? [ add-undo-move ] unless

    dot-offset 0 > [
        mark [ copy-dot mark! ] unless
        dot-offset 1- dot-offset!
        dot-offset goal-column!
        t repaint!
    ] [
        dot-line-prev [
            mark [ copy-dot mark! ] unless
            dot-line-prev dot-line!
            dot-line-length dot-offset!
            dot-offset goal-column!
            reframe? [
                dot-line top-line!
            ] when
            t repaint!
        ] when
    ] if ;

: move-right
    last-command ' move-right eq? [ add-undo-move ] unless

    mark [
        end-of-block
        unmark
    ] return-if

    dot-offset dot-line-length < [
        dot-offset 1+ dot-offset!
        dot-offset goal-column!
    ] [
        dot-line-next [
            dot-line-next dot-line!
            0 dot-offset!
            dot-offset goal-column!
            reframe? [
                dot-line-number textview-rows 1- - nth-line top-line!
                t repaint!
            ] when
        ] when
    ] if ;

: select-right
    last-command ' select-right eq? [ add-undo-move ] unless

    dot-offset dot-line-length < [
        mark [ copy-dot mark! ] unless
        dot-offset 1+ dot-offset!
        dot-offset goal-column!
        t repaint!
    ] [
        dot-line-next [
            mark [ copy-dot mark! ] unless
            dot-line-next dot-line!
            0 dot-offset!
            dot-offset goal-column!
            reframe? [
                dot-line-number textview-rows 1- - nth-line top-line!
            ] when
            t repaint!
        ] when
    ] if ;

: move-left-to-beginning-of-word
    // no undo
    in-word? [
        [ char-left-line [ in-word? ] [ f ] if ] loop
        dot-offset zero? [ dot-offset 1+ dot-offset! ] unless
    ] when ;

: move-left-to-beginning-of-whitespace
    // no undo
    in-whitespace? [
        [ char-left-line [ in-whitespace? ] [ f ] if ] loop
        in-whitespace? [ dot-offset 1+ dot-offset! ] unless
    ] when ;

: move-left-past-whitespace
    // no undo
    [ char-left-line [ in-whitespace? ] [ f ] if ] loop ;

: word-left
    at-bof? ?exit

    reset-redo
    add-undo-move

    at-bol? [
        unmark
        char-left-internal
    ] return-if

    // not bol
    // move back one character
    char-left-line assert
    at-eol? assert-false

    {
        {
            [ in-whitespace? ] [
                move-left-past-whitespace
                move-left-to-beginning-of-word
            ]
        }

        { [ in-word? ] [ move-left-to-beginning-of-word ] }

        [
            // not in word, not in whitespace
            // if previous char is whitespace, then dot is at the start
            // of a word and we are done
            dot-char-prev whitespace? [
                // previous char is NOT whitespace
                [
                    char-left-line
                    [ not-in-word? not-in-whitespace? and ] [ f ] if
                ] loop
                dot-offset zero? [ dot-offset 1+ dot-offset! ] unless
                // skip over preceding whitespace if any
                move-left-past-whitespace
                dot-offset zero? [ dot-offset 1+ dot-offset! ] unless
            ] unless
        ]
    } cond
;

: move-right-past-whitespace
    // no undo
    [ char-right-line [ in-whitespace? ] [ f ] if ] loop
;

: word-right
    at-eof? ?exit

    reset-redo
    add-undo-move

    {
        {
            [ at-eol? ]
            [ dot-line next>> dot-line! 0 dot-offset! maybe-reframe ]
        }

        {
            [ in-whitespace? ]
            [ move-right-past-whitespace ]
        }

        {
            [ in-word? ]
            [
                // move to end of word
                [ not-in-word? at-eol? or ] [
                    dot-offset 1+ dot-offset!
                ] until
                // skip following whitespace if any
                in-whitespace? [
                    move-right-past-whitespace
                ] when
            ]
        }

        [
            // not eol, not in word, not in whitespace
            [ in-whitespace? in-word? or at-eol? or ] [ dot-offset 1+ dot-offset! ] until
            in-whitespace? [
                move-right-past-whitespace
            ] when
        ]
    } cond ;

: do-down
    dot-line-next null? ?exit

    reset-redo

    last-command ' do-down eq? [ add-undo-move ] unless

    mark [
        end-of-block
        unmark
    ] when

    dot-line-next [
        dot-line!
        reframe? [
            dot-line-number textview-rows 1- - nth-line top-line!
            t repaint!
        ] when
        adjust-dot-column
    ] when* ;

: do-up
    reset-redo

    dot-line-prev [
        last-command ' do-up eq? [ add-undo-move ] unless
        dot-line-prev dot-line!
        unmark
        reframe? [
            dot-line top-line!
            t repaint?!
        ] when
        adjust-dot-column
    ] when
;

feral-config:winui? feral-config:gtkui? or #if

: mousewheel-scroll-up
    current-command last-command!
    ' mousewheel-scroll-up current-command!

    top-line prev>> [
        top-line!
        4 [ top-line prev>> [ top-line! ] when* ] times

        dot-line-number bottom-line-number > [
            last-command ' mousewheel-scroll-up eq? [ add-undo-move ] unless
            bottom-line dot-line!
            0 dot-offset!
        ] when

        t repaint?!
        update-display
    ] when* ;

: mousewheel-scroll-down
    current-command last-command!
    ' mousewheel-scroll-down current-command!

    top-line next>> [
        top-line!
        4 [ top-line next>> [ top-line! ] when* ] times

        dot-line-number top-line-number < [
            last-command ' mousewheel-scroll-down eq? [ add-undo-move ] unless
            top-line dot-line!
            0 dot-offset!
        ] when

        t repaint?!
        update-display
    ] when* ;

#endif

: do-window-up
    top-line prev>> [
        top-line prev>> top-line!
        dot-line-number bottom-line-number > [
            add-undo-move
            bottom-line dot-line!
            0 dot-offset!
        ] when
        t repaint?!
    ] when
;

: do-window-down
    top-line next>> [
        top-line next>> top-line!
        dot-line-number top-line-number < [
            add-undo-move
            top-line dot-line!
            0 dot-offset!
        ] when
        t repaint?!
    ] when
;

: do-page-down
    add-undo-move

    unmark

    dot-row :> goal-row

    top-line-number textview-rows + last-line-number > [
        last-line dot-line!
        adjust-dot-column
    ] return-if

    top-line :> line!
    0 :> i!

    [ line next>> i textview-rows < and ] [
        i 1+ i! line next>> line!
    ] while

    line top-line!

    goal-row i!

    [ line next>> i 0 > and ] [
        i 1- i! line next>> line!
    ] while

    line dot-line!

    t repaint!
    adjust-dot-column
;

: do-page-up
    add-undo-move

    unmark

    dot-row :> goal-row

    dot-line :> line!
    0 :> i!

    [ line prev>> i textview-rows < and ] [
        i 1+ i! line prev>> line!
    ] while

    line dot-line!

    goal-row i!

    [ line prev>> i 0 > and ] [
        i 1- i! line prev>> line!
    ] while

    line top-line!

    t repaint!
    adjust-dot-column
;
