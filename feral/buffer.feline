//  Copyright (C) 2017-2019 Peter Graves <gnooth@gmail.com>

//  This program is free software: you can redistribute it and/or modify
//  it under the terms of the GNU General Public License as published by
//  the Free Software Foundation, either version 3 of the License, or
//  (at your option) any later version.

//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//  GNU General Public License for more details.

//  You should have received a copy of the GNU General Public License
//  along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feral-core feline accessors modes mini ;
in: editor

: buffer-directory ( buf )              // buffer -> directory
    buf filename>> [ file-name-directory ] [ get-current-directory ] if* ;

: buffer-append-line ( line buf )       // line buffer -> void
    buf last-line>> line prev<<
    buf last-line>> [ line swap next<< ] when*
    line buf last-line<<
    buf first-line>> [
        line buf first-line<<
    ] unless
;

: buffer-set-contents ( s buf )         // string buffer -> void
    f buf first-line<<
    f buf last-line<<
    f buf top-line<<
    f buf dot<<
    f buf mark<<

    s string-lines [ make-line/2 buf buffer-append-line ] each-index
;

: create-buffer                         // void -> void
    make-buffer :> buf
    "" 0 make-line/2

    [ buf buffer-append-line ]
    [ 0 make-position buf dot<< ]
    bi

    buf buffer-list vector-push
    buf current-buffer!

    dot-line top-line!
;

: create-buffer/2                       // filename line-number -> void
    verify-index  :> line-number
    verify-string :> filename

    make-buffer :> buf

    filename buf filename<<

    filename file-name-nondirectory buf name<<

    filename file-name-extension [ modes at buf mode<< ] when*

    filename file-contents :> contents

    contents string-lines verify-vector :> lines

    contents empty? [
        "" lines vector-push
    ] [
        // detect eol
        '\r' contents string-index CR+LF LF ? buf eol<<
        contents string-last-char '\n' eq? [
            "" lines vector-push
        ] when
    ] if

    lines [
        make-line/2 buf buffer-append-line
    ] each-index

    buf first-line>> line? assert
    buf last-line>>  line? assert

    buf first-line>> 0 make-position buf dot<<
    buf first-line>> buf top-line<<

    buf buffer-list vector-push

    buf set-current-buffer

    line-number 1- 0 max line-count 1- min nth-line dot-line!

    0 dot-offset!

    buf dup mode>> initialize-buffer

    t repaint! ;

: find-buffer-from-file-name ( filename )       // filename -> buffer/f
    buffer-list [ filename>> filename = ] find  // index/f element/f
    nip ;

: next-buffer
    next-buffers vector-?pop [
        current-buffer previous-buffers vector-push
        current-buffer!
        t repaint!
    ] when* ;

: previous-buffer
    previous-buffers vector-?pop [
        current-buffer next-buffers vector-push
        current-buffer!
        t repaint!
    ] when* ;

: buffer-name ( buf )
    buf name>> [ buf filename>> ] unless* ;

use: feral-config

winui? #if
use: mini
#endif

: confirm-kill-current-buffer           // -> ?
    f :> confirmed?!

    modified? [
        "Save file? (y or n) " minibuffer-accept-string
        "y" = [ do-save ] when
    ] when

    modified? [
        "Abandon changes? (yes or no) " minibuffer-accept-string
        "yes" = [ t confirmed?! ] when
    ] when

    confirmed?
;

winui? #if
unuse: mini
#endif

: kill-current-buffer
    buffer-list [ current-buffer eq? ] find     // -> index/f element/f
    current-buffer assert-eq                    // -> index
    :> n!

    n buffer-list vector-remove-nth!

    n buffer-list length < [ n 1- n! ] unless

    n buffer-list vector-nth current-buffer!
    t repaint?!
;

: kill-buffer
    f :> confirmed?!

    modified? [ confirm-kill-current-buffer confirmed?! ] when

    modified? not confirmed? or [
        buffer-list length 1 = [
            0 buffer-list vector-set-length
            exit-feral
        ] [
            kill-current-buffer
        ] if
    ] when
;
