// Copyright (C) 2018-2019 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feral-core feline accessors segments modes ansi-color feral-config ;
in: feline-mode

private

tuple: feline-mode
    formatter ;

: identifier-char?                      // char -> ?
    '\s' neq?
;

: feline-mode-identifier-at-dot         // void -> start/f string/f

    { [ mark ] [ mark-line dot-line eq? ] } && [
        mark dot make-region/2 begin-offset
    ] [
        dot-offset dot-line-length 1- min 0 max
    ] if :> offset

    offset dot-line-text [ identifier-char? not ] find-last-from drop :> start!

    // get to first non-blank char
    start [ start 1+ start! ] [ 0 start! ] if

    start dot-line-text string-skip-to-whitespace // -> index/f
    dot-line-length or
    [
        start swap dot-line-text string-substring // -> string
        start swap                                // -> start string
    ] [
        f f
    ] if* ;

method: feline-mode identifier-at-dot   // void -> start/f string/f
    drop
    feline-mode-identifier-at-dot ;

{
    "'"
    "("
    ")"
    ":"
    ":>"
    ";"
    "["
    "]"
    "constant"
    "defer"
    "f"
    "generic"
    "global"
    "in:"
    "method:"
    "private"
    "public"
    "special:"
    "symbol:"
    "syntax:"
    "t"
    "test:"
    "tuple:"
    "use:"
    "using:"
    "var"
    "vector{"
    "{"
    "}"
} constant syntax-words

{
    ":"
    "constant"
    "defer"
    "generic"
    "global"
    "method:"
    "syntax:"
    "test:"
    "tuple:"
    "var"
} constant defining-words

{
    "bi"
    "bi@"
    "case"
    "cleave"
    "cond"
    "each"
    "each-index"
    "each-integer"
    "filter"
    "find"
    "find-from"
    "if"
    "if*"
    "keep"
    "return-if"
    "times"
    "tri"
    "unless"
    "unless*"
    "until"
    "when"
    "when*"
    "while"
} constant combinators

32 code-char constant space-char
34 code-char constant quote-char

: non-blank?                            // string -> ?
    1 ?enough
    verify-string
    [ space-char neq? ] find drop ;

: skip-quoted-string                    // start-index string -> index/f
    2dup string-length >= [ 2drop f ] return-if

    :> s
    :> start

    s start 1+ string-tail <string-iterator> :> iter

    f :> this-char!
    this-char :> last-char!
    f :> done?!

    [
        this-char last-char!
        iter string-iterator-next this-char!
        this-char [ t done?! ] unless
        done?
    ] [
        this-char '\x22' eq? [
            last-char '\' neq? [
                t done?!
            ] when
        ] when
    ] until

    start 1+ iter string-iterator-index +
;

: string->segments ( s )                // string -> segments
    s string-length :> len
    0 :> start!
    0 :> end!
    16 <vector> :> v
    f :> c!

    [ end len fixnum-fixnum< ] [
        start s string-nth c!
        {
            { [ c space-char eq? ] [ start s string-skip-whitespace len or end! ] }
            { [ c quote-char eq? ] [ start s skip-quoted-string len or end! ] }
            [ start s string-skip-to-whitespace len or end! ]
        } cond
        start end s string-substring start make-segment v vector-push
        end start!
    ] while

    v
;

winui? #if

  0 255 255 make-colorref constant color-syntax
255 165   0 make-colorref constant color-name
  0 170   0 make-colorref constant color-string
  0 153 153 make-colorref constant color-combinator
255   0   0 make-colorref constant color-preprocessor

#else

192 192 192 rgb-foreground-prefix constant text-prefix
128 128   0 rgb-foreground-prefix constant comment-prefix
  0 255 255 rgb-foreground-prefix constant syntax-prefix
255 165   0 rgb-foreground-prefix constant name-prefix
  0 170   0 rgb-foreground-prefix constant string-prefix
  0 153 153 rgb-foreground-prefix constant combinator-prefix
255   0   0 rgb-foreground-prefix constant preprocessor-prefix

#endif

var in-comment?

var last-segment-text

winui? #if

: winui-format-segment ( segment )
    in-comment? [
        segment text>> { "--" "//" "///" } member? in-comment?!
    ] unless

    in-comment? [
        color-comment segment format<<
    ] return-if

    // not a comment
    last-segment-text defining-words member? [
        color-name segment format<<
    ] return-if

    segment text>> syntax-words member? [
        color-syntax segment format<<
    ] return-if

    segment text>> string-first-char quote-char eq? [
        color-string segment format<<
    ] return-if

    segment text>> combinators member? [
        color-combinator segment format<<
    ] return-if

    segment text>> { "#if" "#else" "#endif" "#ifdef" "#ifndef" } member? [
        color-preprocessor segment format<<
    ] return-if

    // default
    color-text segment format<<
;

: winui-format-segments ( segments )
    f in-comment?!
    "" last-segment-text!
    segments [
        dup winui-format-segment
        text>> dup non-blank? [ last-segment-text! ] [ drop ] if
    ] each

    segments
;

: winui-feline-format-line              // string -> segments
    string->segments
    winui-format-segments
;

public

: initialize-feline-mode
    feline-mode make-instance :> m
    ' winui-feline-format-line m formatter<<
    m feline-extension modes set-at
;

#else

: format-segment ( segment )            // segment -> void
    in-comment? [
        segment text>> { "--" "//" "///" } member? in-comment?!
    ] unless

    in-comment? [
        comment-prefix segment format<<
    ] return-if

    // not a comment
    last-segment-text defining-words member? [
        name-prefix segment format<<
    ] return-if

    segment text>> syntax-words member? [
        syntax-prefix segment format<<
    ] return-if

    segment text>> string-first-char quote-char eq? [
        string-prefix segment format<<
    ] return-if

    segment text>> combinators member? [
        combinator-prefix segment format<<
    ] return-if

    segment text>> { "#if" "#else" "#endif" "#ifdef" "#ifndef" } member? [
        preprocessor-prefix segment format<<
    ] return-if

    // default
    text-prefix segment format<<
;

: format-segments ( segments )          // segments -> segments
    f in-comment?!
    "" last-segment-text!
    segments [
        dup format-segment
        text>> dup non-blank? [ last-segment-text! ] [ drop ] if
    ] each
;

: feline-format-line                    // string -> segments
    string->segments                    // -> vector
    dup format-segments
;

public

: initialize-feline-mode
    feline-mode make-instance :> m
    ' feline-format-line m formatter<<
    m feline-extension modes set-at
;

#endif

initialize-feline-mode
