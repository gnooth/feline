// Copyright (C) 2018-2020 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feral-core feline accessors segments modes feral-colors ;
in: feline-mode

private

global instance

: get-instance                          // void -> mode
    instance ;

: feline-mode-identifier-char?          // char -> ?
    char? [ '\s' neq? ] [ nil ] if* ;

"// " constant feline-mode-comment-start

feline-mode-comment-start length constant comment-start-length

: feline-mode-toggle-line-comment
    dot-line-text :> text
    text indentation-string :> indentation-string
    indentation-string length :> indentation-length
    indentation-length text tail :> content

    editor:add-undo-line-edit

    feline-mode-comment-start content head? [
        // uncomment
        indentation-string
        comment-start-length content tail +
        dot-line-text!
        {
            {
                [ dot-offset indentation-length comment-start-length + >= ]
                [ dot-offset comment-start-length - dot-offset! ]
            }
            {
                [ dot-offset indentation-length >= ]
                [ indentation-length dot-offset! ]
            }
            // dot-offset < indent-length
            [ ]
        } cond
    ] [
        // comment
        indentation-string
        feline-mode-comment-start +
        content +
        dot-line-text!
        dot-offset indentation-length >= [
            dot-offset comment-start-length + dot-offset!
        ] when
    ] if

    modified
    true dot-line-changed?! ;

: expand-internal ( abbrev expansion )  // abbrev expansion -> void
    mark ?exit
    abbrev dot-line-head tail? not ?exit
    editor:add-undo-line-edit
    abbrev length dot-line-head head*
    expansion +
    abbrev +
    dot-line-tail +
    dot-line-text!
    dot-offset abbrev length - 2 + dot-offset!
    true dot-line-changed?! ;

: expand-1 ( abbrev )                   // abbrev -> void
    abbrev "[  ] " expand-internal ;

: expand-2 ( abbrev )                   // abbrev -> void
    abbrev "[  ] [  ] " expand-internal ;

: expand                                // void -> void
    mark ?exit
    dot-offset 0? ?exit
    dot-offset :> old-offset
    dot-offset 1- dot-offset!
    [ in-word? dot-offset 0? not and ] [ dot-offset 1- dot-offset! ] while
    not-in-word? [ dot-offset 1+ dot-offset! ] when
    dot-offset old-offset eq? ?exit
    dot-offset old-offset dot-line-text string-substring :> abbrev
    old-offset dot-offset!
    abbrev {
        "if"        => [ abbrev expand-2 ]
        "if*"       => [ abbrev expand-2 ]
        "recover"   => [ abbrev expand-2 ]
        "unless"    => [ abbrev expand-1 ]
        "unless*"   => [ abbrev expand-1 ]
        "until"     => [ abbrev expand-2 ]
        "when"      => [ abbrev expand-1 ]
        "when*"     => [ abbrev expand-1 ]
        "while"     => [ abbrev expand-2 ]
        _           => [ abbrev "No expansion for %S" format message ]
    } match ;

32 code-char constant space-char
34 code-char constant quote-char

: non-blank?                            // string -> ?
    1 ?enough
    verify-string
    [ space-char neq? ] find drop ;

: skip-quoted-string                    // start-index string -> index/nil
    2dup string-length >= [ 2drop nil ] ?return

    :> s
    :> start

    start 1+ s string-tail make-string-iterator :> iter

    local ch

    [
        iter string-iterator-next ch!
        ch dup '"' neq? and
    ] loop
    // reached end
    ch [
        ch '"' assert-eq
        iter string-iterator-index 1+
    ] [
        iter string-iterator-string string-length
    ] if :> end

    start 1+ end + ;

: string-segments ( s ) // string -> segments
    s string-length :> len
    0 !> start
    0 !> end
    16 <vector> :> v
    nil !> c

    [ end len fixnum-fixnum< ] [
        start s string-nth c!
        {
            { [ c space-char eq? ] [ start s string-skip-whitespace len or end! ] }
            { [ c quote-char eq? ] [ start s skip-quoted-string len or end! ] }
            [ start s string-skip-to-whitespace len or end! ]
        } cond
        start end s string-substring start make-segment v vector-push
        end start!
    ] while

    v
;

var in-comment?

var last-segment-text

: format-segment ( segment )
    segment text>> :> s

    in-comment? [
        s { "--" "//" "///" } member? in-comment?!
    ] unless

    in-comment? [
        color-comment segment format<<
    ] ?return

    // not a comment
    last-segment-text defining-words member? [
        color-name segment format<<
    ] ?return

    s syntax-words member? [
        color-syntax segment format<<
    ] ?return

    s string-first-char :> first-char

    first-char quote-char eq? [
        color-string segment format<<
    ] ?return

    s combinators member? [
        color-combinator segment format<<
    ] ?return

    s { "#if" "#else" "#endif" "#ifdef" "#ifndef" } member? [
        color-preprocessor segment format<<
    ] ?return

    first-char ':' eq? [
        color-keyword segment format<<
    ] ?return

    [ first-char '\'' eq? ] [ s string-last-char '\'' eq? ] both? [
        color-string segment format<<
    ] ?return

    // default
    color-text segment format<< ;

: format-segments ( segments )
    nil in-comment?!
    "" last-segment-text!
    segments [
        dup format-segment
        text>> dup non-blank? [ last-segment-text! ] [ drop ] if
    ] each
    segments ;

: feline-mode-format-line // line -> segments
    text>> detab string-segments format-segments ;

: line-is-definition? ( line )
    line text>> string-trim-head :> s
    '\s' s string-index :> end
    end nil? [ nil ] ?return
    0 end s string-substring defining-words member? ;

: feline-mode-list-symbols
    ' feline-mode:line-is-definition? list-symbols:list-symbols ;

use: completion

: feline-mode-provide-completion
    1 ?enough verify-string :> prefix

    current-completion [ next-completion ] ?return

    all-words
    [ symbol-name prefix swap string-has-prefix? ] filter
    [ symbol-name ] map
    :> strings

    prefix strings initialize-completion
    next-completion ;

: feline-mode-find-definition

    "find-definition" history:set-history

    ' feline-mode-provide-completion mini:set-completion-provider
    nil current-completion!

    "Word: " mini:minibuffer-accept-string
    [ editor:find-definition-internal ] when* ;

unuse: completion

global feline-mode-local-map

feral-config:winui? feral-config:gtkui? or #if

use: feral-key

: initialize-local-map
    128 <hashtable> :> m
    ' feline-mode-toggle-line-comment   ctrl-/          m set-at
    ' expand                            ctrl-space      m set-at
    ' feline-mode-list-symbols          ctrl-shift-o    m set-at
    ' feline-mode-find-definition       ctrl-t          m set-at
    m feline-mode-local-map! ;

initialize-local-map

unuse: feral-key

#endif

: feline-mode-initialize-buffer         // buffer -> void
    1 ?enough :> buf
    feline-mode-local-map buf local-map<<
    instance indent-size>> buf indent-size<< ;

: initialize-feline-mode
    mode make-instance :> m
    "Feline" m name<<
    4 m indent-size<<
    feline-mode-comment-start m comment-start<<
    ' feline-mode-initialize-buffer m initialize-buffer-function<<
    ' feline-mode-identifier-char? m identifier-char-function<<
    ' feline-mode-format-line m format-line-function<<
    m feline-extension modes set-at
    m instance! ;

initialize-feline-mode
