// Copyright (C) 2019-2020 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: modes feral-core feline accessors segments feral-colors ;
in: c-mode

private

global instance

: get-instance // void -> mode
    instance ;

: scan-neutral ( char iter )
    char '/' neq? [ :neutral ] ?return
    iter string-iterator-peek :> char2
    char2 '/' eq? [ :line-comment ] ?return
    char2 '*' eq? [ :block-comment ] [ :neutral ] if ;

: scan-block-comment ( char iter )
    char '*' neq? [ :block-comment ] ?return
    iter string-iterator-peek '/' eq? [
        2 iter string-iterator-skip
        :neutral
    ] [ :block-comment ] if ;

: scan-line ( line ) // line -> state
    line text>> make-string-iterator :> iter
    line state>> !> state
    local char
    [
        iter string-iterator-next char!
        char
    ] [
        {
            { [ state :neutral eq? ] [ char iter scan-neutral state! ] }
            { [ state :block-comment eq? ] [ char iter scan-block-comment state! ] }
            {
                [ state :line-comment eq? ]
                [ iter string-iterator-skip-to-end :neutral state! ]
            }
        } cond
    ] while
    state ;

: scan-buffer ( buf )                   // buffer -> void
    buf first-line>> !> line
    :neutral !> state
    [ line ] [
        state line state<<
        line scan-line state!
        line next>> line!
    ] while
    nil buf needs-scan<< ;

: c-mode-format-line ( line )           // line -> segments
    line text>> detab :> text
    text make-string-iterator :> iter
    line state>> !> state

    local char
    0 !> start-index
    16 make-vector :> v

    [
        iter string-iterator-next char!
        char
    ] [
        state {
            :neutral => [
                char iter scan-neutral state!
                state { :block-comment :line-comment } member-eq? [
                    start-index iter string-iterator-index text substring
                    start-index color-text make-segment/3 v push
                    iter string-iterator-index start-index!
                ] when
            ]
            :block-comment => [
                char iter scan-block-comment state!
                state :neutral eq? [
                    start-index iter string-iterator-index text substring
                    start-index color-comment make-segment/3 v push
                    iter string-iterator-index start-index!
                ] when
            ]
            :line-comment => [ ]        // nothing to do
            _ => [
                ?nl "c-mode-format-line unsupported state " write state .
                "unsupported" error
            ]
        } match
    ] while

    start-index iter string-iterator-index text substring
    start-index
    state :block-comment eq? state :line-comment eq? or [ color-comment ] [ color-text ] if
    make-segment/3 v push

    state :line-comment? eq? [ :neutral state! ] when

    v ;

: c-mode-initialize-buffer ( buf )      // buffer -> void
    get-instance :> m
    m indent-size>> buf indent-size<<
    true buf needs-scan<< ;

: initialize-c-mode
    mode make-instance :> m
    "C" m name<<
    2 m indent-size<<
    "// " m comment-start<<
    ' c-mode-initialize-buffer m initialize-buffer-function<<
    ' c-mode-format-line m format-line-function<<
    ' scan-buffer m scan-buffer-function<<
    m ".c" modes set-at
    m ".h" modes set-at

    // FIXME when (if?) we have a C++ mode
    m ".cpp" modes set-at
    m ".cc" modes set-at

    m instance! ;

initialize-c-mode
