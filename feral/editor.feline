// Copyright (C) 2017-2019 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feline ;
in: feral-config

empty

f constant winui?

in: editor

empty

"feral-loader.feline" load

using: feral-core feline accessors ;

: do-mouse                              // key -> void
    dup fixnum? not [ drop ] return-if

    :> keycode
    f :> button! f :> x! f :> y!
//     keycode fixnum>hex log
    keycode 24 rshift 0x1b5b4d = [
        // "\e[M"
        keycode 0xff bitand 32 - y!
        keycode 0xff00 bitand 8 rshift 32 - x!
        keycode 0xff0000 bitand 16 rshift 3 bitand zero? [
            // left button pressed
//             x y "left button pressed x = %d y = %d" format log
            top-line-number y + gotoline-internal
            x 1- 0 max dot-line-length min dot-offset!
        ] when
    ] when
;

: do-command ( key )                    // key -> ?

    f :> command!

    // try local map first
    current-buffer local-map>> :> local-map
    local-map [
        key local-map at command!
    ] when

    // global map
    command [
        key keymaps:lookup-key command!
    ] unless

    command [
        command dup current-command! call-symbol
        t
    ] [
//         linux? [ key do-mouse ] when
        f
    ] if ;

: dispatch ( key )                      // key -> void
    current-command last-command!
    f current-command!

    key do-command [
        key printable-char? [ key insert-normal-char ] when
    ] unless ;

: edit-loop
    f done?!
    [ done? ] [
        update-display
        ekey
        mini:clear-message
        dispatch
    ] until

    // clean up
    f current-buffer!
    f buffer-list!
    gc
;

: edit1
    use-alternate-screen-buffer

    page
    normal
    maybe-reframe
    t repaint?!

    linux? [
        // enable mouse tracking
        "\e[?1000h" write-string-escaped
    ] when

    edit-loop

    clear-status-line
    mini:clear-message
    0 textview-rows at-xy
    show-cursor

    use-default-screen-buffer
;

: ed
    use-alternate-screen-buffer
    page
    normal
    t repaint?!
    edit-loop
    clear-status-line
    mini:clear-message
    0 textview-rows at-xy
    show-cursor
    use-default-screen-buffer
;

"feral-main.feline" load
