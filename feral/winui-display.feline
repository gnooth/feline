// Copyright (C) 2017-2020 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feral-core segments feline accessors ;
in: editor

feral-config:winui? assert

: update-caret-pos
    dot-column textview-char-width *
    dot-line-number top-line-number - textview-char-height *
    textview-set-caret-pos ;

: update-mode-line mode-line-text modeline-set-text ;

: repaint
    winui-hide-caret

    top-line !> line
    0 !> row

    [ line row textview-rows <= and ] [
        line row repaint-line
        row 1+ row! line next>> line!
    ] while

    [ row textview-rows <= ] [
        0 row textview-clear-eol
        row 1+ row!
    ] while

    nil repaint?!
    clear-changed-lines

    winui-show-caret ;

: repaint-changed-lines
    winui-hide-caret

    top-line !> line
    0 !> row

    [ line row textview-rows <= and ] [
        line changed-lines member-eq? [
            line row repaint-line
        ] when
        row 1+ row! line next>> line!
    ] while

    [ row textview-rows <= ] [
        0 row textview-clear-eol
        row 1+ row!
    ] while

    clear-changed-lines

    winui-show-caret ;

: update-display-internal

    // FIXME needs-renumber [ renumber ] when
    renumber

    repaint? [
        repaint
    ] [
        dot-line-changed? [
            dot-line line-changed
            nil dot-line-changed?!
        ] when
        repaint-changed-lines
    ] if
    update-mode-line
    update-caret-pos ;

: update-display
    [ update-display-internal ] [
        last-error string? [
            last-error
            [ feral-config:termui? [ print ] unless ]
            [ message ]
            [ logging:log ]
            tri
        ] when
        [ print-backtrace ] logging:with-output-to-log
    ] recover ;

: request-update-display
    winui-request-update-display ;
