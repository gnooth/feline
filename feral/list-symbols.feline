// Copyright (C) 2019 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feral-core feline accessors ;
in: list-symbols

empty

: list-symbols                          // callable -> void
    1 ?enough verify-callable :> line-is-definition-function

    // reuse existing buffer if possible
    nil !> buf
    buffer-list [
        dup mode>> symbols-mode:get-instance eq?
        swap parent>> current-buffer eq? and
    ] find
    // -> index/nil element/nil
    [ nip dup editor:clear-buffer ] [ drop make-buffer ] if* buf!

    current-buffer filename>> file-name-nondirectory " [symbols]" + buf name<<

    first-line !> source-line
    nil !> goal // put dot on this line in the [symbols] buffer

    [ source-line ] [
        source-line line-is-definition-function call [
            source-line number>> 1+ source-line text>> "%d %s" format make-line/1
            goal [
                source-line number>> dot-line-number > [
                    buf last-line>> goal!
                ] when
            ] unless
            buf buffer-append-line
        ] when
        source-line next>> source-line!
    ] while

    buf first-line>> buf top-line<<
    goal [ buf last-line>> goal! ] unless
    goal 0 make-position buf dot<<

    t buf read-only<<

    current-buffer buf parent<<
    current-buffer directory>> buf directory<<
    symbols-mode:get-instance buf mode<<
    buf mode>> initialize-buffer-function>> [ buf swap call ] when*

    buf buffer-list vector-push
    buf set-current-buffer

    renumber
    maybe-reframe
    t repaint?! ;
