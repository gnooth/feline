// Copyright (C) 2017-2019 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feral-core feline accessors segments feral-colors ;
in: editor

feral-config:gtkui? assert

192 192 192 make-colorref constant fg-default
  0   0   0 make-colorref constant bg-default
 64  64  64 make-colorref constant bg-selected
 32  32  32 make-colorref constant bg-current-line

: bg-set-default bg-default textview-set-bg-color ;

: bg-set-selected bg-selected textview-set-bg-color ;

: bg-set-current-line bg-current-line textview-set-bg-color ;

: repaint-segment                       // segment row -> void
    2 ?enough
    :> row :> segment

    segment format>> textview-set-fg-color
    segment start>> row segment text>> textview-text-out ;

: repaint-segments ( segments row )     // segments row -> void
    segments [ row repaint-segment ] each ;

: repaint-line-no-selection ( line row ) // line row -> void
    // no part of line is selected
    line dot-line eq? [ bg-set-current-line ] [ bg-set-default ] if
    line format-line :> segments
    segments row repaint-segments
    segments detabbed-text-length row textview-clear-eol
    bg-set-default ;

: repaint-selected-line ( line row )    // line row -> void
    // entire line is selected
    line format-line :> segments
    bg-set-selected
    segments row repaint-segments
    segments detabbed-text-length row textview-clear-eol
    bg-set-default ;

: repaint-segment-of-partly-selected-line ( segment line region row )
    line region begin-line eq? [ region begin position-column ] [ 0 ] if
    :> startcol // region start column

    line region end-line eq?
    [ region end position-column ]
    [ line dup length line-offset-column ] if
    :> endcol   // region end column

    segment start>> startcol >
    segment start>> segment length + endcol <= and [
        // segment is entirely within selection
        bg-set-selected
        segment row repaint-segment
    ] ?return

    local seg1
    local seg2

    startcol segment in-segment? endcol segment in-segment? and [
        // region is entirely within segment
        segment startcol segment start>> - split-segment seg2! seg1!
        bg-set-default
        seg1 row repaint-segment

        // now split seg2 at endcol
        seg2 endcol seg2 start>> - split-segment
        swap
        bg-set-selected
        row repaint-segment
        bg-set-default
        row repaint-segment
    ] ?return

    startcol segment in-segment? [
        segment startcol segment start>> - split-segment seg2! seg1!
        bg-set-default
        seg1 row repaint-segment
        bg-set-selected
        seg2 row repaint-segment
        bg-set-default
    ] ?return

    endcol segment in-segment? [
        segment endcol segment start>> - split-segment seg2! seg1!
        bg-set-selected
        seg1 row repaint-segment
        bg-set-default
        seg2 row repaint-segment
    ] ?return

    segment row repaint-segment ;

: repaint-partly-selected-line ( line row )
    line format-line :> segments
    mark dot make-region/2 :> region
    line line-starts-in-selection? [ bg-set-selected ] [ bg-set-default ] if
    segments [ line region row repaint-segment-of-partly-selected-line ] each

    [ line next>> ]
    [ line next>> 0 make-position region in-region? ]
    both?
    bg-selected bg-default ? textview-set-bg-color

    segments detabbed-text-length row textview-clear-eol ;

: repaint-line ( line row )             // line row -> void
    {
        {
            [ line line-selected? ]
            [ line row repaint-selected-line ]
        }
        {
            [ line line-is-partly-selected? ]
            [ line row repaint-partly-selected-line ]
        }
        // otherwise...
        [ line row repaint-line-no-selection ]
    } cond ;

: repaint
    top-line !> line
    0 !> row

    [ line row textview-rows < and ] [
        line row repaint-line
        row 1+ row! line next>> line!
    ] while

    nil repaint?! ;

: update-caret-pos
    dot-column
    dot-line-number top-line-number -
    textview-set-caret-pos ;

: update-mode-line mode-line-text modeline-set-text ;

: update-display
    update-caret-pos
    update-mode-line ;
