// Copyright (C) 2019-2020 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feline accessors feral-core segments modes feral-colors ;
in: diff-mode

private

global instance

: diff-mode-goto-source
    dot-line !> line
    nil !> found
    0 !> count

    [ line ] [
        "@@" line text>> head? [
            line found! nil line!
        ] [
            "-" line text>> head? [ count 1+ count! ] unless
            line prev>> line!
        ] if
    ] while

    found nil? ?exit

    found text>> :> s
    '+' s string-find-char 1+ s string-tail :> s2
    ',' s2 string-find-char s2 head :> s3

    s3 string>number count +
    1- 0 max                            // convert to zero-based line number
    :> linenumber

    // REVIEW parent buffer may have been killed
    editor:switch-to-parent-buffer

    linenumber editor:goto-line-internal
    true repaint?! ;

global diff-mode-local-map

: initialize-local-map
    128 <hashtable> :> m
    ' editor:kill-buffer feral-key:escape m set-at
    ' diff-mode-goto-source feral-key:enter m set-at
    m diff-mode-local-map! ;

initialize-local-map

feral-config:winui? feral-config:gtkui? or #if
' diff-mode-goto-source feral-key:double-mouse-1 diff-mode-local-map set-at
#endif

: diff-mode-format-line ( s ) // string -> segments

    s length 0? [ s trivial-format-line ] ?return

    "+++" s head? [ s trivial-format-line ] ?return
    "---" s head? [ s trivial-format-line ] ?return

    s string-first-char {
        { '+' [ color-insertion ] }
        { '-' [ color-deletion ] }
        [
            // default case
            drop
            color-text
        ]
    } case :> color

    s 0 color make-segment/3 1array ;

: diff-mode-initialize-buffer           // buffer -> void
    1 ?enough :> buf
    diff-mode-local-map buf local-map<< ;

: initialize-diff-mode
    mode make-instance :> m
    "Diff" m name<<
    ' diff-mode-initialize-buffer m initialize-buffer-function<<
    ' diff-mode-format-line m formatter<<
    m instance! ;

initialize-diff-mode

public

: get-instance                          // void -> mode
    instance ;
