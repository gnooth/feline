// Copyright (C) 2019 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: feline accessors feral-core segments modes feral-colors feral-config ;
in: diff-mode

private

tuple: diff-mode
    formatter ;

method: diff-mode formatter             // mode -> formatter
    formatter>> ;

: diff-mode-goto-source
    dot-line :> line!
    f :> found!
    0 :> count!

    [ line ] [
        "@@" line text>> head? [
            line found! f line!
        ] [
            "-" line text>> head? [ count 1+ count! ] unless
            line prev>> line!
        ] if
    ] while

    found null? ?exit

    found text>> :> s
    '+' s string-find-char 1+ s string-tail :> s2
    ',' s2 string-find-char s2 head :> s3

    s3 string>number count +
    1- 0 max                            // convert to zero-based line number
    :> line-number

    current-buffer parent>> current-buffer!
    line-number editor:gotoline-internal
    t repaint! ;

global diff-mode-local-map

: initialize-local-map
    128 <hashtable> :> m
    ' editor:kill-buffer key:escape m set-at
    ' diff-mode-goto-source key:enter m set-at
    m diff-mode-local-map! ;

initialize-local-map

method: diff-mode initialize-buffer // buffer mode -> void
    2 ?enough
    drop
    diff-mode-local-map swap local-map<< ;

winui? #if

0x00 0x99 0x00 make-colorref constant color-insertion
0x99 0x00 0x00 make-colorref constant color-deletion

#else

use: ansi-color

0x00 0x99 0x00 rgb-foreground-prefix constant color-insertion
0x99 0x00 0x00 rgb-foreground-prefix constant color-deletion

unuse: ansi-color

#endif

: diff-mode-format-line ( s ) // string -> segments

    s length 0 eq? [ s trivial-format-line ] return-if

    "+++" s head? [ s trivial-format-line ] return-if
    "---" s head? [ s trivial-format-line ] return-if

    s string-first-char {
        { '+' [ color-insertion ] }
        { '-' [ color-deletion ] }
        [
            // default case
            drop
            color-text
        ]
    } case :> color

    s 0 color make-segment/3 1array ;

public

: initialize-diff-mode                  // void -> diff-mode
    diff-mode make-instance :> m
    ' diff-mode-format-line m formatter<<
    m ;
