// Copyright (C) 2019-2020 Peter Graves <gnooth@gmail.com>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

using: modes feral-core feline accessors segments feral-colors ;
in: rust-mode

private

{
    "as"
    "async"
    "await"
    "break"
    "const"
    "continue"
    "crate"
    "dyn"
    "else"
    "enum"
    "extern"
    "false"
    "fn"
    "for"
    "if"
    "impl"
    "in"
    "let"
    "loop"
    "match"
    "mod"
    "move"
    "mut"
    "pub"
    "ref"
    "return"
    "self"
    "static"
    "struct"
    "super"
    "trait"
    "true"
    "type"
    "unsafe"
    "use"
    "where"
    "while"
} constant syntax-words

{
    "u8"    "i8"
    "u16"   "i16"
    "u32"   "i32"
    "u64"   "i64"
    "u128"  "i128"

    "f32"   "f64"
    "isize" "usize"
    "bool"
    "str"   "char"
} constant special-types // rust-mode.el

global instance

: get-instance // void -> mode
    instance ;

: parse-quoted-string ( iter )          // string-iterator -> segment
    iter string-iterator-index :> start
    local ch
    [
        iter string-iterator-next ch!
        ch dup '"' neq? and
    ] loop
    // reached end
    ch [
        ch '"' assert-eq
        1 iter string-iterator-skip
        iter string-iterator-index
    ] [
        iter string-iterator-string string-length
    ] if :> end

    start end iter string-iterator-string substring
    start color-string make-segment/3 ;

: parse-line-comment ( iter )           // string-iterator -> segment
    iter string-iterator-index !> start
    start 0? [
        iter string-iterator-string 0 color-comment make-segment/3
    ] [
        start
        iter string-iterator-string string-length
        iter string-iterator-string substring
        start color-comment make-segment/3
    ] if ;

global last-identifier

: parse-identifier ( iter )             // string-iterator -> segment
    iter string-iterator-index :> start

    local ch
    [
        iter string-iterator-next ch!
        [ ch char? ] [ ch identifier-char? ] both?
    ] loop
    // reached end of identifier
    iter string-iterator-index :> end
    start end iter string-iterator-string substring :> identifier

    {
        { [ last-identifier "fn" = ] [ color-name ] }
        { [ identifier syntax-words member? ] [ color-syntax ] }
        { [ identifier special-types member? ] [ color-type ] }
        { [ 0 identifier string-nth-unsafe char-upper-case? ] [ color-type ] }
        [
            // default
            color-text
        ]
    } cond :> format

    identifier last-identifier!

    identifier start format make-segment/3 ;

: rust-mode-format-line ( line )        // line -> vector-of-segments
    line text>> detab :> text
    text make-string-iterator :> iter
    line state>> !> state
    100 make-vector :> v
    0 !> start

    local char
    local end

    nil last-identifier!

    [
        iter string-iterator-next char!
        char
    ] [
        state {
            :neutral => [
                char {
                    '"' =>  [
                        iter string-iterator-index end!
                        start end iter string-iterator-string substring
                        start color-text make-segment/3 v push
                        iter parse-quoted-string v push
                        iter string-iterator-index start!
                    ]
                    '/' => [
                        iter string-iterator-peek '/' eq? [
                            iter string-iterator-index start > [
                                iter string-iterator-index end!
                                start end iter string-iterator-string substring
                                start color-text make-segment/3 v push
                            ] when
                            iter parse-line-comment v push
                            iter string-iterator-skip-to-end
                            iter string-iterator-index start!
                        ] [
                            iter string-iterator-peek '*' eq? [
                                iter string-iterator-index start > [
                                    iter string-iterator-index end!
                                    start end iter string-iterator-string substring
                                    start color-text make-segment/3 v push
                                ] when
                                :block-comment state!
                            ] when
                        ] if
                    ]
                    _ => [
                        char identifier-char?
                        [
                            iter string-iterator-index end!
                            start end iter string-iterator-string substring
                            start color-text make-segment/3 v push
                            iter parse-identifier v push
                            iter string-iterator-index start!
                        ] when
                    ]
                } match
            ]
            :block-comment => [
                char iter c-mode:scan-block-comment state!
                state :neutral eq? [
                    start iter string-iterator-index text substring
                    start color-comment make-segment/3 v push
                    iter string-iterator-index start!
                ] when
            ]
            _ => [ ]
        } match
    ] while

    iter string-iterator-string length end!
    start end < [
        start end iter string-iterator-string substring
        start
        state { :block-comment :line-comment } member-eq? [ color-comment ] [ color-text ] if
        make-segment/3 v push
    ] when

    v ;

: line-is-definition? ( line )
    "fn " line text>> string-has-prefix? [ true ] ?return

    " fn " line text>> substring-start !> offset

    offset nil? [ nil ] ?return

    "//" line text>> substring-start [
        offset < [ nil offset! ] when
    ] when*

    offset ;

: list-symbols
    ' line-is-definition? list-symbols:list-symbols ;

global local-map

feral-config:winui? feral-config:gtkui? or #if

: initialize-local-map
    128 <hashtable> :> m
    ' list-symbols feral-key:ctrl-shift-o m set-at
    m local-map! ;

initialize-local-map

#endif

: initialize-buffer                     // buffer -> void
    1 ?enough :> buf
    local-map buf local-map<<
    get-instance :> m
    m indent-size>> buf indent-size<< ;

: initialize-rust-mode
    mode make-instance :> m
    "Rust" m name<<
    4 m indent-size<<
    "// " m comment-start<<
    ' initialize-buffer m initialize-buffer-function<<
    ' rust-mode-format-line m format-line-function<<

    // REVIEW c-mode
    ' c-mode:scan-buffer m scan-buffer-function<<

    m ".rs" modes set-at
    m instance! ;

initialize-rust-mode
